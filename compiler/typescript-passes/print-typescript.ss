;;; This file is part of Compact.
;;; Copyright (C) 2025 Midnight Foundation
;;; SPDX-License-Identifier: Apache-2.0
;;; Licensed under the Apache License, Version 2.0 (the "License");
;;; you may not use this file except in compliance with the License.
;;; You may obtain a copy of the License at
;;;
;;; 	http://www.apache.org/licenses/LICENSE-2.0
;;;
;;; Unless required by applicable law or agreed to in writing, software
;;; distributed under the License is distributed on an "AS IS" BASIS,
;;; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
;;; See the License for the specific language governing permissions and
;;; limitations under the License.

#!chezscheme

(define-pass print-typescript : Ltypescript (ir) -> Ltypescript ()
  (definitions
    (define sourcemap-tracker)
    ;; The inner-key blob as a hex literal, taken from the same converter the
    ;; ZKIR pass used, so the contract carries the bytes whose digest the circuit
    ;; committed to rather than re-deriving them from the file.
    (define (inner-vk-hex vk)
      (let ([convert (inner-verifying-key-blob)])
        (unless convert
          (internal-errorf 'print-typescript "no inner verifying-key converter is installed"))
        (format "'0x~(~{~2,'0x~}~)'" (bytevector->u8-list (car (convert vk))))))
    (define compact-stdlib-entries
       '(
         "addField"
         "assert"
         "CompactError"
         "convertBigintToBytes"
         "convertBytesToField"
         "convertBytesToUint"
         "convertNumericToJubjubScalar"
         "crossContractCall"
         "decodeContractAddress"
         "mulField"
         "secp256k1BaseAdd"
         "secp256k1BaseMul"
         "secp256k1BaseSub"
         "secp256k1ScalarAdd"
         "secp256k1ScalarMul"
         "secp256k1ScalarSub"
         "subField"
         "typeError"
         "verifyProof"
         ))
    (define (compact-stdlib name)
      (unless (member name compact-stdlib-entries)
        (internal-errorf 'print-typescript "~s is not listed in compact-stdlib-entries" name))
      (format "__compactRuntime.~a" name))
    (module (with-local-unique-names demand-unique-local-name! unique-global-name unique-local-name
             format-internal-binding format-id-reference)
      (define unique-id-names (make-eq-hashtable))
      (define global-ht (make-hashtable string-hash string=?))
      (define local-ht #f)
      (define (make-local-ht ht)
        (if ht
            (hashtable-copy ht #t)
            (let ([ht (make-hashtable string-hash string=?)])
              ; amateur javascript language design makes "eval" and "arguments" invalid as parameter names
              (hashtable-set! ht "eval" #t)
              (hashtable-set! ht "arguments" #t)
              ht)))
      (define-syntax with-local-unique-names
        (syntax-rules ()
          [(_ b1 b2 ...)
           (fluid-let ([local-ht (make-local-ht local-ht)])
             b1 b2 ...)]))
      (define (demand-unique-local-name! uname)
        (let ([global-a (hashtable-cell global-ht uname #f)]
              [local-a (hashtable-cell local-ht uname #f)])
          (when (or (cdr global-a) (cdr local-a))
            (internal-errorf 'register-local-name! "~a is already taken" uname))
          (set-cdr! local-a #t)))
      (define (unique-global-name str)
        (let loop ([suffix 0])
          (let ([uname (format "_~a_~s" str suffix)])
            (let ([global-a (hashtable-cell global-ht uname #f)]
                  [local-a (and local-ht (hashtable-cell local-ht uname #f))])
              (if (or (cdr global-a) (and local-a (cdr local-a)))
                  (loop (fx+ suffix 1))
                  (begin
                    (set-cdr! global-a #t)
                    uname))))))
      (define (unique-local-name str)
        (assert local-ht)
        (let loop ([suffix 0])
          (let ([uname (format "~a_~s" str suffix)])
            (let ([global-a (hashtable-cell global-ht uname #f)]
                  [local-a (hashtable-cell local-ht uname #f)])
              (if (or (cdr global-a) (cdr local-a))
                  (loop (fx+ suffix 1))
                  (begin
                    (set-cdr! local-a #t)
                    uname))))))
      (define (format-internal-binding unique-name id)
        (let ([name (unique-name (symbol->string (id-sym id)))])
          (eq-hashtable-set! unique-id-names id name)
          name))
      (define (format-id-reference id)
        (let ([a (eq-hashtable-cell unique-id-names id #f)])
          (or (cdr a)
              (internal-errorf 'format-id-reference "format-internal-binding has not yet been called on ~s" id)))))
    (define (format-function-reference src function-name)
      (format "this.~a" (format-id-reference function-name)))
    (define (arg->id arg)
      (nanopass-case (Ltypescript Argument) arg
        [(,var-name ,type) var-name]))
    (define (arg->type arg)
      (nanopass-case (Ltypescript Argument) arg
        [(,var-name ,type) type]))
    (define exported-type-ht)
    (define-record-type tinfo
      (nongenerative)
      (fields export-name tvar* type))
    (define helper*)
    (define-syntax precedence-table
      (lambda (x)
        (define (parse-levels level*)
          (let f ([level* level*] [i 0])
            (if (null? level*)
                '()
                (syntax-case (car level*) ()
                  [(op ...)
                   (andmap identifier? #'(op ...))
                   (fold-right
                     (lambda (op alist)
                       (cons (cons op i) alist))
                     (f (cdr level*) (fx+ i 1))
                     #'(op ...))]))))
        (syntax-case x ()
          [(_ name level ...)
           (with-syntax ([alist (parse-levels #'(level ...))])
             #'(define-syntax name
                 (lambda (x)
                   (define (go op)
                     (cond
                       [(assq op 'alist) => cdr]
                       [else (syntax-error x "unrecognized operator")]))
                   (syntax-case x (add1)
                     [(_ op) (go (datum op))]
                     [(_ add1 op) (add1 (go (datum op)))]))))])))
    (precedence-table precedence
      (none)
      (comma)
      (=)
      (if)
      (or)
      (and)
      (== !=)
      (< <= > >=)
      (as)
      (+ -)
      (* %)
      (not)
      (new call struct-ref vector-ref))
    (define-datatype XPelt
      (XPelt-exported-circuit src internal-id arg* type stmt external-name* pure?)
      (XPelt-internal-circuit src internal-id arg* type stmt pure?)
      (XPelt-witness src internal-id arg* type external-name)
      (Xpelt-native-circuit src internal-id native-entry arg* type external-name pure?)
      (XPelt-type-definition src type-name export-name tvar-name* type)
      (XPelt-public-ledger pl-array lconstructor external-names)
      (XPelt-ledger-kernel))
    (define (xpelt->uname xpelt)
      (XPelt-case xpelt
        [(XPelt-exported-circuit src internal-id arg* type stmt external-name* pure?)
         (format-internal-binding unique-global-name internal-id)]
        [(XPelt-internal-circuit src internal-id arg* type stmt pure?)
         (format-internal-binding unique-global-name internal-id)]
        [(XPelt-witness src internal-id arg* type external-name)
         (format-internal-binding unique-global-name internal-id)]
        [(Xpelt-native-circuit src internal-id native-entry arg* type external-name pure?)
         (format-internal-binding unique-global-name internal-id)]
        [(XPelt-type-definition src type-name export-name tvar-name* type) #f]
        [(XPelt-public-ledger pl-array lconstructor external-names) #f]
        [(XPelt-ledger-kernel) #f]))
    ;; Which functions have async wrappers in the generated JS. Only impure circuits, since their
    ;; bodies may `await` a cross-contract call; everything else is synchronous.
    (define function-async-ht (make-eq-hashtable))
    (define (mark-function-async! function-name)
      (eq-hashtable-set! function-async-ht function-name #t))
    (define (function-async? function-name)
      (eq-hashtable-ref function-async-ht function-name #f))

    (module (descriptor-table type->maybe-descriptor-name type->descriptor-name)
      (define descriptor-table #f)

      (define (type->maybe-descriptor-name type)
        (assert descriptor-table)
        (cond
          [(hashtable-ref descriptor-table type #f) => format-id-reference]
          [else #f]))

      (define (type->descriptor-name type)
        (assert descriptor-table)
        (format-id-reference (assertf (hashtable-ref descriptor-table type #f) "unregistered type descriptor for ~s" type))))

    (define (pl-array->public-bindings pl-array)
      (let f ([pl-array pl-array] [pb* '()])
        (nanopass-case (Ltypescript Public-Ledger-Array) pl-array
          [(public-ledger-array ,pl-array-elt* ...)
           (fold-right
             (lambda (pl-array-elt pb*)
               (nanopass-case (Ltypescript Public-Ledger-Array-Element) pl-array-elt
                 [,pl-array (f pl-array pb*)]
                 [,public-binding (cons public-binding pb*)]))
             pb*
             pl-array-elt*)])))

    (define (construct-typed-value descriptor-name q)
      (make-Qconcat
        "{ "
        ((make-Qsep ",")
         (make-Qconcat
           "value: "
           (make-Qconcat
             (format "~a.toValue(" descriptor-name)
             q
             ")"))
         (format "alignment: ~a.alignment()" descriptor-name))
        " }"))

    (module (construct-query vminstr->q-array make-vmref)
      (define (vminstr->q-array src vminstr*)
        (make-Qconcat
           "["
           1 (apply (make-Qsep ",")
                    (fold-right
                      (lambda (vminstr q*)
                        (guard (c [(suppressed-condition? c) q*])
                          (cons (let ([op (vminstr-op vminstr)] [arg* (vminstr-arg* vminstr)])
                                  (if (null? arg*)
                                      (format "'~a'" op)
                                      (make-Qconcat
                                        "{ " op ": { "
                                        (apply (make-Qsep ",")
                                               (map (lambda (arg)
                                                      (let ([name (car arg)] [val (cdr arg)])
                                                        (make-Qconcat name ": " (construct-query-value src val #t))))
                                                    arg*))
                                        " } }")))
                                q*)))
                      '()
                      vminstr*))
           "]"))
      (define-record-type vmref
        (nongenerative)
        (fields type q)
        (protocol
          (lambda (new)
            (lambda (type q)
              (new type q)))))
      (define-condition-type &suppressed &condition make-suppressed-condition suppressed-condition?)
      (define (construct-query-value src v top-level?)
        (cond
          [(eq? v #f) "false"]
          [(eq? v #t) "true"]
          [(and (integer? v) (exact? v)) (format "~d" v)]
          [(list? v)
           (make-Qconcat
             "["
             1 (apply
                 (make-Qsep ",")
                 (map
                   (lambda (v)
                     (let ([is-stack (and (VMop? v) (VMop-case v
                                                      [(VMstack) #t]
                                                      [else #f]))])
                       (if is-stack
                           (construct-query-value src v #f)
                           (make-Qconcat
                             "{ "
                             ((make-Qsep ",")
                              "tag: 'value'"
                              (make-Qconcat
                                "value: "
                                (construct-query-value src v #f)))
                             " }"))))
                   v))
             "]")]
          [(vmref? v)
           (let ([v-type (vmref-type v)] [q (vmref-q v)])
             (nanopass-case (Ltypescript Type) (de-alias v-type)
               [(tadt ,src ,adt-name ([,adt-formal* ,adt-arg*] ...) ,vm-expr (,adt-op* ...) (,adt-rt-op* ...))
                ; FIXME: at present, we can assume that whenever we passs a value of some
                ; public-adt as a query argument, it must be the result of default<public-adt>,
                ; since that's all that can get past the type checker.  if we generalize to
                ; allow first-class public-adt values, this code will no longer be valid.
                (construct-query-value src
                  (expand-vm-expr
                    src
                    (map cons adt-formal* adt-arg*)
                    (vm-expr-expr vm-expr))
                  top-level?)]
               [else (construct-typed-value (type->descriptor-name v-type) q)]))]
          [(VMop? v)
           (VMop-case v
             [(VMstack) "{ tag: 'stack' }"]
             [(VMvoid) "undefined"]
             [(VMsuppress) (raise (make-suppressed-condition))]
             [(VM+ x y)
              (make-Qconcat
                "("
                (construct-query-value src x #f)
                "+"
                (construct-query-value src y #f)
                ")")]
             [(VMvalue->int v)
              (make-Qconcat
                "parseInt(__compactRuntime.valueToBigInt("
                2 (construct-query-value src v #f)
                4 ".value"
                0 "))")]
             [(VMalign value bytes)
              ; emit-version uses u32 in ocrt ==> (= bytes 4)
              (assert (or (= bytes 1) (= bytes 4) (= bytes 8) (= bytes 16)))
              (construct-typed-value
                (type->descriptor-name (with-output-language (Ltypescript Type) `(tunsigned ,src ,(- (expt 2 (* bytes 8)) 1))))
                (format "~dn" value))]
             [(VMnull type)
              (nanopass-case (Ltypescript Type) (de-alias type)
                [(tadt ,src ,adt-name ([,adt-formal* ,adt-arg*] ...) ,vm-expr (,adt-op* ...) (,adt-rt-op* ...))
                 (construct-query-value src
                   (expand-vm-expr
                     src
                     (map cons adt-formal* adt-arg*)
                     (vm-expr-expr vm-expr))
                   top-level?)]
                [else
                 (construct-typed-value
                   (type->descriptor-name type)
                   (Expr (with-output-language (Ltypescript Expression) `(default ,src ,type))
                         (precedence add1 comma)
                         #f))])]
             [(VMmax-sizeof type)
              (assert (not (public-adt? type)))
              (make-Qconcat
                "Number(__compactRuntime.maxAlignedSize("
                2 (type->descriptor-name type)
                2 ".alignment()"
                0 "))")]
             [(VMstate-value-cell val)
              (make-Qconcat
                "__compactRuntime.StateValue.newCell("
                (construct-query-value src val #f)
                (if top-level?
                    ").encode()"
                    ")"))]
             [(VMstate-value-ADT val v-type)
              (if (public-adt? v-type)
                  (construct-query-value src val top-level?)
                  (make-Qconcat
                    "__compactRuntime.StateValue.newCell("
                    (construct-query-value src val #f)
                    (if top-level?
                        ").encode()"
                        ")")))]
             [(VMstate-value-null)
              (if top-level?
                  "__compactRuntime.StateValue.newNull().encode()"
                  "__compactRuntime.StateValue.newNull()")]
             [(VMstate-value-map key* val*)
              (make-Qconcat
                "__compactRuntime.StateValue.newMap("
                2 (apply (make-Qsep ",")
                    "new __compactRuntime.StateMap()"
                    (map (lambda (key val)
                           ; at present, midnight-ledger.ss doesn't create maps with arguments
                           (make-Qconcat
                             ".insert("
                             ((make-Qsep ",")
                              (construct-query-value src key #f)
                              (construct-query-value src val #f))
                             ")"))
                         key*
                         val*))
                0 (if top-level?
                      ").encode()"
                      ")"))]
             [(VMstate-value-merkle-tree nat key* val*)
              (make-Qconcat
                "__compactRuntime.StateValue.newBoundedMerkleTree("
                2 (apply (make-Qsep ",")
                    (make-Qconcat
                      "new __compactRuntime.StateBoundedMerkleTree("
                      (construct-query-value src nat #f)
                      ")")
                    (map (lambda (key val)
                           ; at present, midnight-ledger.ss doesn't create MerkleTrees with arguments
                           (make-Qconcat
                             ".update("
                             ((make-Qsep ",")
                              (construct-query-value src key #f)
                              (construct-query-value src val #f))
                             ")"))
                         key*
                         val*))
                0 (if top-level?
                      ").encode()"
                      ")"))]
             [(VMstate-value-array val*)
              (make-Qconcat
                "__compactRuntime.StateValue.newArray()"
                2 (apply make-Qconcat
                         (map (lambda (val)
                                (make-Qconcat
                                  ".arrayPush("
                                  (construct-query-value src val #f)
                                  ")"))
                              val*))
                2 (if top-level?
                      ".encode()"
                      ""))]
             [(VMaligned-concat x*)
              (make-Qconcat
                "__compactRuntime.alignedConcat("
                2 (apply (make-Qsep ",")
                         (map (lambda (x) (construct-query-value src x #f)) x*))
                0 (if top-level?
                      ").encode()"
                      ")"))]
             [(VMcoin-commit coin recipient)
              (make-Qconcat
                "__compactRuntime.runtimeCoinCommitment("
                2 ((make-Qsep ",")
                    (construct-query-value src coin #f)
                    (construct-query-value src recipient #f))
                0 ")")]
             [(VMleaf-hash x)
              (make-Qconcat
                "__compactRuntime.leafHash("
                2 (construct-query-value src x #f)
                0 ")")])]
          [else (internal-errorf 'construct-query-value src "unhandled case ~s" v)]))
      (define (construct-vm-instructions src path-elt* adt-formal* adt-arg* adt-op expr*)
        (nanopass-case (Ltypescript ADT-Op) adt-op
          [(,ledger-op ,op-class (,adt-name (,adt-formal* ,adt-arg*) ...) ((,var-name* ,type*) ...) ,type ,vm-code)
           (assert (fx= (length expr*) (length var-name*)))
           (let ([vminstr* (expand-vm-code
                             src
                             (map (lambda (path-elt)
                                    (nanopass-case (Ltypescript Path-Element) path-elt
                                      [,path-index (VMalign path-index 1)]
                                      [(,src ,type ,expr) (make-vmref type (Expr expr (precedence add1 comma) #f))]))
                                  path-elt*)
                             #f
                             (append (map cons adt-formal* adt-arg*)
                                     (map (lambda (var-name type expr)
                                            (let ([sym (id-sym var-name)])
                                              (cons sym (make-vmref type expr))))
                                          var-name*
                                          type*
                                          expr*))
                             (vm-code-code vm-code))])
             (vminstr->q-array src vminstr*))]))

      (define (coin-recipient-indices adt-op)
        (nanopass-case (Ltypescript ADT-Op) adt-op
          [(,ledger-op (,ledger-op-class ,nat ,nat^) (,adt-name (,adt-formal* ,adt-arg*) ...) ((,var-name* ,type*) ...) ,type ,vm-code)
           (list nat nat^)]))

      (define (should-check-coin-commitment? adt-op)
        (nanopass-case (Ltypescript ADT-Op) adt-op
          [(,ledger-op (,ledger-op-class ,nat ,nat^) (,adt-name (,adt-formal* ,adt-arg*) ...) ((,var-name* ,type*) ...) ,type ,vm-code)
           (eq? ledger-op-class 'update-with-coin-check)]
          [else #f]))

      (define (coin-commitment-check adt-op expr*)
        (let* ([indices (coin-recipient-indices adt-op)]
               [coin-idx (car indices)]
               [recipient-idx (cadr indices)]
               [coin-expr (list-ref expr* coin-idx)]
               [recipient-expr (list-ref expr* recipient-idx)])
          (make-Qconcat
           "__compactRuntime.hasCoinCommitment("
           ((make-Qsep ",") "context" coin-expr recipient-expr)
           ")")))

      (define (construct-query src path-elt* adt-formal* adt-arg* adt-op expr*)
        (let ([query (make-Qconcat/src src
                       "__compactRuntime.queryLedgerState("
                       ((make-Qsep ",")
                        "context"
                        "partialProofData"
                        (construct-vm-instructions src path-elt* adt-formal* adt-arg* adt-op expr*))
                       ")")])
          (if (should-check-coin-commitment? adt-op)
              (make-Qconcat
                (coin-commitment-check adt-op expr*)
                " ? "
                query
                " : "
                (format "(() => { throw new __compactRuntime.CompactError(`~a: Coin commitment not found. Check the coin has been received (or call 'createZswapOutput')`); })()" (format-source-object src)))
              query))
        ))

    (define (has-read? adt-op*)
      (ormap (lambda (adt-op)
               (and (eq? (op-name adt-op) 'read)
                    (is-runtime-op? adt-op)
                    adt-op))
             adt-op*))
    (define (op-name adt-op)
      (if (Ltypescript-ADT-Op? adt-op)
          (nanopass-case (Ltypescript ADT-Op) adt-op
            [(,ledger-op ,op-class (,adt-name (,adt-formal* ,adt-arg*) ...) ((,var-name* ,type*) ...) ,type ,vm-code)
             ledger-op])
          (nanopass-case (Ltypescript ADT-Runtime-Op) adt-op
            [(,ledger-op (,arg* ...) ,result-type ,runtime-code)
            ledger-op])))
    (define (is-runtime-op? adt-op)
      (or (not (Ltypescript-ADT-Op? adt-op))
          (nanopass-case (Ltypescript ADT-Op) adt-op
            [(,ledger-op ,op-class (,adt-name (,adt-formal* ,adt-arg*) ...) ((,var-name* ,type*) ...) ,type ,vm-code)
             (eq? op-class 'read)])))
    (define (maybe-iterator op-name)
      (if (eq? op-name 'iter)
          "[Symbol.iterator]"
          (to-camel-case (symbol->string op-name) #f)))
    (define (exported-public-binding? public-binding)
      (nanopass-case (Ltypescript Public-Ledger-Binding) public-binding
        [(,src ,ledger-field-name (,path-index* ...) ,type)
         (id-exported? ledger-field-name)]))
    (define (subst-tcontract type)
      (nanopass-case (Ltypescript Type) (de-alias type)
        [(tcontract ,src ,contract-name (,elt-name* ,pure-dcl* (,type** ...) ,type*) ...)
         (with-output-language (Ltypescript Type)
           `(tstruct ,src ContractAddress (bytes (tbytes ,src 32))))]
        [else type]))

    (define (print-contract.d.ts src xpelt* uname*)
      ;; Every entry in `Circuits`, `ImpureCircuits`, and `ProvableCircuits` is an async wrapper
      (define (circuit-result-type type)
        (make-Qconcat "Promise<__compactRuntime.CircuitResults<PS, " (Type type) ">>"))
      (define (print-exported-impure-circuit-declaration do-me?)
        (lambda (xpelt uname)
          (XPelt-case xpelt
            [(XPelt-exported-circuit src internal-id arg* type stmt external-name* pure?)
             (when (do-me? pure?)
               (for-each
                 (lambda (external-name)
                   (with-local-unique-names
                     (demand-unique-local-name! "context")
                     (print-Q 2
                       (make-Qconcat
                         external-name
                         "("
                         (apply (make-Qsep ",")
                           "context: __compactRuntime.CircuitContext<PS>"
                           (map Typed-Argument arg*))
                         "): "
                         (circuit-result-type type)
                         ";")))
                   (newline))
                 external-name*))]
            [else (void)])))
      (define (print-exported-pure-circuit-declaration xpelt uname)
        (XPelt-case xpelt
          [(XPelt-exported-circuit src internal-id arg* type stmt external-name* pure?)
           (when pure?
             (for-each
               (lambda (external-name)
                 (with-local-unique-names
                   (print-Q 2
                     (make-Qconcat
                       external-name
                       "("
                       (apply (make-Qsep ",") (map Typed-Argument arg*))
                       "): "
                       (Type type)
                       ";")))
                 (newline))
               external-name*))]
          [else (void)]))
      (define (print-exported-provable-circuit-declaration)
        (lambda (xpelt uname)
          (XPelt-case xpelt
            [(XPelt-exported-circuit src internal-id arg* type stmt external-name* pure?)
             (for-each
               (lambda (external-name)
                 (when (memq (string->symbol external-name) (proof-circuit-names))
                   (with-local-unique-names
                     (demand-unique-local-name! "context")
                     (print-Q 2
                       (make-Qconcat
                         external-name
                         "("
                         (apply (make-Qsep ",")
                           "context: __compactRuntime.CircuitContext<PS>"
                           (map Typed-Argument arg*))
                         "): "
                         (circuit-result-type type)
                         ";")))
                   (newline)))
               external-name*)]
            [else (void)])))
      (define (print-exported-circuit-declaration xpelt uname)
        (XPelt-case xpelt
          [(XPelt-exported-circuit src internal-id arg* type stmt external-name* pure?)
           (apply (print-exported-impure-circuit-declaration
                    (if pure?
                        (lambda (x) x)
                        not))
                  (list xpelt uname))]
          [else (void)]))
      (define (print-witness-declaration xpelt uname)
        (XPelt-case xpelt
          [(XPelt-witness src internal-id arg* type external-name)
           (with-local-unique-names
             (demand-unique-local-name! "private_state")
             (print-Q 2
               (make-Qconcat
                 external-name
                 "("
                 (apply (make-Qsep ",")
                   "context: __compactRuntime.WitnessContext<Ledger, PS>"
                   (map Typed-Argument arg*))
                 "): "
                 "[PS, " (Type type) "]"
                 ";")))
           (newline)]
          [else (void)]))

      (module (print-ledger-declaration)
        (define (op-signature-Q adt-op)
          (nanopass-case (Ltypescript ADT-Op) adt-op
            [(,ledger-op ,op-class (,adt-name (,adt-formal* ,adt-arg*) ...) ((,var-name* ,type*) ...) ,type ,vm-code)
             (with-local-unique-names
               (let ([formal* (map (lambda (var-name) (format-internal-binding unique-local-name var-name))
                                   var-name*)])
                 (apply list
                   "("
                   (apply (make-Qsep ",")
                     (map (lambda (formal type)
                            (assert (not (public-adt? type)))
                            (make-Qconcat formal ": " (Type type)))
                          formal*
                          type*))
                   "): "
                   (if (public-adt? type)
                       (nanopass-case (Ltypescript Type) (de-alias type)
                         [(tadt ,src ,adt-name ([,adt-formal* ,adt-arg*] ...) ,vm-expr (,adt-op* ...) (,adt-rt-op* ...))
                          (let* ([all-op* (filter is-runtime-op? (append adt-op* adt-rt-op*))]
                                 [all-op* (cond [(has-read? all-op*) => list] [else all-op*])])
                            (list
                              "{"
                              2 (ledger-field-decl-Q src adt-arg* all-op*)
                              0 "}"))])
                       (list (Type type))))))]))
        (define (rt-op-return-type adt-arg* result-type)
          (let ([targs (map (lambda (adt-arg)
                              (nanopass-case (Ltypescript Public-Ledger-ADT-Arg) adt-arg
                                [,nat (number->string nat)]
                                [,type
                                 (if (public-adt? type)
                                     ; at present, this case should be ruled out by the ledger meta-type checks
                                     "undefined"
                                     (with-output-to-string
                                       (lambda ()
                                         (print-Q 0 (Type type)))))]))
                            adt-arg*)])
            (apply result-type "__compactRuntime." targs)))
        (define (rt-op-signature-Q adt-rt-op adt-arg*)
          (nanopass-case (Ltypescript ADT-Runtime-Op) adt-rt-op
            [(,ledger-op (,arg* ...) ,result-type ,runtime-code)
             (with-local-unique-names
               (let ([formal* (map Untyped-Argument arg*)])
                 (list
                   "("
                   (apply (make-Qsep ",")
                     (map (lambda (formal arg)
                            (make-Qconcat formal ": " (Type (arg->type arg))))
                          formal*
                          arg*))
                   "): "
                   (rt-op-return-type adt-arg* result-type))))]))
        (define (ledger-field-decl-Q src adt-arg* all-op*)
          (apply (make-Qsep ";")
            (map (lambda (op)
                   (with-local-unique-names
                     (apply make-Qconcat
                       (maybe-iterator (op-name op))
                       (if (Ltypescript-ADT-Op? op)
                           (op-signature-Q op)
                           (rt-op-signature-Q op adt-arg*)))))
                 (remp (lambda (op)
                         ; run-time ops like iterators that expect to be supplied a descriptor
                         ; for use in decoding values.  we don't have a decoder for adt values, and if we did,
                         ; it's not clear what they would do with an adt value anyway.  so we just don't
                         ; generate such runtime ops.
                         (and (Ltypescript-ADT-Runtime-Op? op)
                              (ormap (lambda (adt-arg)
                                       (nanopass-case (Ltypescript Public-Ledger-ADT-Arg) adt-arg
                                         [,type (public-adt? type)]
                                         [else #f]))
                                     adt-arg*)))
                       all-op*))))
        (define (print-public-binding public-binding external-names)
          (nanopass-case (Ltypescript Public-Ledger-Binding) public-binding
            [(,src ,ledger-field-name (,path-index* ...) ,type)
             (nanopass-case (Ltypescript Type) (de-alias type)
               [(tadt ,src^ ,adt-name ([,adt-formal* ,adt-arg*] ...) ,vm-expr (,adt-op* ...) (,adt-rt-op* ...))
                (for-each
                  (lambda (export-name)
                    (print-Q 2
                      (let* ([all-op* (filter is-runtime-op? (append adt-op* adt-rt-op*))]
                             [maybe-read (has-read? all-op*)])
                        (if maybe-read
                            (make-Qconcat
                              "readonly "
                              export-name
                              ": "
                              (if (Ltypescript-ADT-Op? maybe-read)
                                  (nanopass-case (Ltypescript ADT-Op) maybe-read
                                    [(,ledger-op ,op-class (,adt-name (,adt-formal* ,adt-arg*) ...) ((,var-name* ,type*) ...) ,type ,vm-code)
                                     (assert (not (public-adt? type)))
                                     (Type type)])
                                  ; at present, no adt-rt-ops qualify (i.e., none are named "read")
                                  (nanopass-case (Ltypescript ADT-Runtime-Op) maybe-read
                                    [(,ledger-op (,arg* ...) ,result-type ,runtime-code)
                                     (rt-op-return-type adt-arg* result-type)]))
                              ";")
                            (make-Qconcat
                              export-name
                              ": {"
                              2 (ledger-field-decl-Q src adt-arg* all-op*)
                              0 "};"))))
                    (newline))
                  (external-names ledger-field-name))]
               [else (assert cannot-happen)])]))
        (define (print-ledger-declaration xpelt uname)
          (XPelt-case xpelt
            [(XPelt-public-ledger pl-array ledger-constructor external-names)
             (for-each
               (lambda (public-binding) (print-public-binding public-binding external-names))
               (filter
                 exported-public-binding?
                 (pl-array->public-bindings pl-array)))]
            [else (void)])))

      (define (print-exported-types xpelt*)
        (for-each
          (lambda (xpelt)
            (XPelt-case xpelt
              [(XPelt-type-definition src type-name export-name tvar-name* type)
               (hashtable-update! exported-type-ht type-name
                 (lambda (ls) (cons (make-tinfo export-name tvar-name* type) ls))
                 '())]
              [else (void)]))
          xpelt*)
        (for-each
          (lambda (xpelt)
            (XPelt-case xpelt
              [(XPelt-type-definition src type-name export-name tvar-name* type)
               (newline)
               (nanopass-case (Ltypescript Type) type
                 [(tcontract ,src ,contract-name (,elt-name* ,pure-dcl* (,type** ...) ,type*) ...)
                   (assert cannot-happen)]
                 [(tstruct ,src ,struct-name (,elt-name* ,type*) ...)
                  (print-Q 0
                    (make-Qconcat
                      "export type "
                      (let ([q (symbol->string export-name)])
                        (if (null? tvar-name*)
                            q
                            (make-Qconcat q "<" (apply (make-Qsep ",") (map symbol->string tvar-name*)) ">")))
                      " = "
                      (make-Qconcat
                        "{ "
                        (apply (make-Qsep ";")
                               (map (lambda (elt-name type)
                                      (make-Qconcat (format "~a" elt-name) ": " (Type type)))
                                    elt-name*
                                    type*))
                        0 "};")))]
                 [(tenum ,src ,enum-name ,elt-name ,elt-name* ...)
                  (print-Q 0
                    (make-Qconcat
                      "export enum "
                      (make-Qconcat (format "~a" export-name))
                      " { "
                      (apply (make-Qsep ",")
                             (let ([elt-name+ (cons elt-name elt-name*)])
                               (map (lambda (elt-name i)
                                      (make-Qconcat (format "~a" elt-name) " = " (format "~d" i)))
                                    elt-name+
                                    (enumerate elt-name+))))
                      0 "}"))]
                 [(talias ,src ,nominal? ,type-name ,type)
                  (print-Q 0
                    (make-Qconcat
                      "export type "
                      (let ([q (symbol->string export-name)])
                        (if (null? tvar-name*)
                            q
                            (make-Qconcat q "<" (apply (make-Qsep ",") (map symbol->string tvar-name*)) ">")))
                      " = "
                      (Type type)
                      ";"))])
               (newline)]
              [else (void)]))
          xpelt*))
      (define (print-constructor-declaration xpelt*)
        (let loop ([xpelt* xpelt*])
          (assert (not (null? xpelt*)))
          (XPelt-case (car xpelt*)
            [(XPelt-public-ledger pl-array lconstructor external-names)
             (nanopass-case (Ltypescript Ledger-Constructor) lconstructor
               [(constructor ,src (,arg* ...) ,stmt)
                (with-local-unique-names
                  (demand-unique-local-name! "context")
                  (print-Q 2
                    (make-Qconcat
                      "initialState("
                      (apply (make-Qsep ",")
                             "context: __compactRuntime.ConstructorContext<PS>"
                             (map (lambda (arg)
                                    (nanopass-case (Ltypescript Argument) arg
                                      [(,var-name ,type)
                                       (make-Qconcat
                                         (format-internal-binding unique-local-name var-name)
                                         ": "
                                         (Type type))]))
                                  arg*))
                      ;; initialState is async too because it can call impure circuits
                      "): Promise<__compactRuntime.ConstructorResult<PS>>;"))
                  (newline))])]
            [else (loop (cdr xpelt*))])))
      (parameterize ([current-output-port (get-target-port 'contract.d.ts)])
        (with-local-unique-names
          (demand-unique-local-name! "T")
          (demand-unique-local-name! "W")
          (fluid-let ([exported-type-ht (make-hashtable symbol-hash eq?)])
            (display-string "import type * as __compactRuntime from '@midnight-ntwrk/compact-runtime';\n")
            (print-exported-types xpelt*)
            (newline)
            (display-string "export type Witnesses<PS> = {\n")
            (for-each print-witness-declaration xpelt* uname*)
            (display-string "}\n")
            (newline)
            (display-string "export type ImpureCircuits<PS> = {\n")
            (for-each (print-exported-impure-circuit-declaration not) xpelt* uname*)
            (display-string "}\n")
            (newline)
            (display-string "export type ProvableCircuits<PS> = {\n")
            (for-each (print-exported-provable-circuit-declaration) xpelt* uname*)
            (display-string "}\n")
            (newline)
            (display-string "export type PureCircuits = {\n")
            (for-each print-exported-pure-circuit-declaration xpelt* uname*)
            (display-string "}\n")
            (newline)
            (display-string "export type Circuits<PS> = {\n")
            (for-each print-exported-circuit-declaration xpelt* uname*)
            (display-string "}\n")
            (newline)
            (display-string "export type Ledger = {\n")
            (for-each print-ledger-declaration xpelt* uname*)
            (display-string "}\n")
            (newline)
            (display-string "export declare class Contract<PS = any, W extends Witnesses<PS> = Witnesses<PS>> {\n")
            (display-string "  witnesses: W;\n")
            (display-string "  circuits: Circuits<PS>;\n")
            (display-string "  impureCircuits: ImpureCircuits<PS>;\n")
            (display-string "  provableCircuits: ProvableCircuits<PS>;\n")
            (display-string "  constructor(witnesses: W);\n")
            (print-constructor-declaration xpelt*)
            (display-string "}\n")
            (newline)
            (display-string "export declare function ledger(state: __compactRuntime.StateValue | __compactRuntime.ChargedState): Ledger;\n")
            (display-string "export declare const pureCircuits: PureCircuits;\n")
            (display-string "export declare const expectedVk: Record<string, string>;\n")
            (display-string "export declare const circuitSignatures: __compactRuntime.CircuitSignatures;\n")
            (display-string "export declare const declaredInterfaces: __compactRuntime.DeclaredInterfaces;\n")
            ))))

    (define (format-field-type ftype)
      (nanopass-case (Ltypescript Field-Type) ftype
        [(field-native) "Field"]
        [(field-scalar (curve-jubjub)) "JubjubScalar"]
        [(field-base (curve-secp256k1)) "Secp256k1Base"]
        [(field-scalar (curve-secp256k1)) "Secp256k1Scalar"]))
    (define (format-point-type ctype)
      (nanopass-case (Ltypescript Curve-Type) ctype
        [(curve-jubjub) "JubjubPoint"]
        [(curve-secp256k1) "Secp256k1Point"]))
    (define (format-type type)
      (nanopass-case (Ltypescript Type) (de-alias type)
        [(tboolean ,src) "Boolean"]
        [(tfield ,src ,ftype) (format-field-type ftype)]
        [(tunsigned ,src ,nat) (format "Uint<0..~d>" (+ nat 1))]
        [(tpoint ,src ,ctype) (format-point-type ctype)]
        [(topaque ,src ,opaque-type) (format "Opaque<~s>" opaque-type)]
        [(tunknown) "Unknown"]
        [(tvector ,src ,len ,type) (format "Vector<~s, ~a>" len (format-type type))]
        [(tbytes ,src ,len) (format "Bytes<~s>" len)]
        [(tcontract ,src ,contract-name (,elt-name* ,pure-dcl* (,type** ...) ,type*) ...)
         (format "contract ~a[~{~a~^, ~}]" contract-name
           (map (lambda (elt-name pure-dcl type* type)
                  (if pure-dcl
                      (format "pure ~a(~{~a~^, ~}): ~a" elt-name
                        (map format-type type*) (format-type type))
                      (format "~a(~{~a~^, ~}): ~a" elt-name
                        (map format-type type*) (format-type type))))
             elt-name* pure-dcl* type** type*))]
        [(ttuple ,src ,type* ...)
         (format "[~{~a~^, ~}]" (map format-type type*))]
        [(tstruct ,src ,struct-name (,elt-name* ,type*) ...)
         (format "struct ~a<~{~a~^, ~}>" struct-name
           (map (lambda (elt-name type)
                  (format "~a: ~a" elt-name (format-type type)))
             elt-name* type*))]
        [(tenum ,src ,enum-name ,elt-name ,elt-name* ...)
         (format "Enum<~a, ~s~{, ~s~}>" enum-name elt-name elt-name*)]
        [(tadt ,src ,adt-name ([,adt-formal* ,adt-arg*] ...) ,vm-expr (,adt-op* ...) (,adt-rt-op* ...))
         (assert cannot-happen)]))

    (module (print-contract.js)
      (define (print-contract-header)
        (display-string "import * as __compactRuntime from '@midnight-ntwrk/compact-runtime';\n")
        (printf "__compactRuntime.checkRuntimeVersion('~a');\n" runtime-version-string)
        (display-string "\n"))

      (define (print-contract-descriptors src descriptor-id* type*)
        (define (print-struct-class class-name elt-name* type*)
          (let ([descriptor-name* (map type->descriptor-name type*)])
            (printf "class ~a {\n" class-name)
            (printf "  alignment() {\n")
            (if (null? elt-name*)
                (printf "    return [];\n")
                (printf "    return ~{~a.alignment()~^.concat(~}~:*~{~*~^)~};\n" descriptor-name*))
            (printf "  }\n")
            (with-local-unique-names
              (let ([value (format-internal-binding unique-local-name (make-temp-id src 'value))])
                (printf "  fromValue(~a) {\n" value)
                (printf "    return {")
                (printf "~{\n      ~a~^,~}"
                        (map (lambda (elt-name descriptor-name)
                               (format "~a: ~a.fromValue(~a)" elt-name descriptor-name value))
                             elt-name*
                             descriptor-name*))
                (printf "\n    }\n")
                (printf "  }\n")))
            (with-local-unique-names
              (let ([value (format-internal-binding unique-local-name (make-temp-id src 'value))])
                (printf "  toValue(~a) {\n" value)
                (if (null? elt-name*)
                    (printf "    return [];\n")
                    (printf "    return ~{~a~^.concat(~}~:*~{~*~^)~};\n"
                            (map (lambda (elt-name descriptor-name)
                                   (format "~a.toValue(~a.~a)" descriptor-name value elt-name))
                                 elt-name*
                                 descriptor-name*)))
                (printf "  }\n")))
            (printf "}\n\n")))
        (define (print-tuple-class tuple-name type*)
          (let ([descriptor-name* (map type->descriptor-name type*)])
            (printf "class ~a {\n" tuple-name)
            (printf "  alignment() {\n")
            (if (null? type*)
                (printf "    return [];\n")
                (printf "    return ~{~a.alignment()~^.concat(~}~:*~{~*~^)~};\n" descriptor-name*))
            (printf "  }\n")
            (with-local-unique-names
              (let ([value (format-internal-binding unique-local-name (make-temp-id src 'value))])
                (printf "  fromValue(~a) {\n" value)
                (printf "    return [")
                (printf "~{\n      ~a~^,~}"
                        (map (lambda (descriptor-name)
                               (format "~a.fromValue(~a)" descriptor-name value))
                             descriptor-name*))
                (printf "\n    ]\n")
                (printf "  }\n")))
            (with-local-unique-names
              (let ([value (format-internal-binding unique-local-name (make-temp-id src 'value))])
                (printf "  toValue(~a) {\n" value)
                (if (null? type*)
                    (printf "    return [];\n")
                    (printf "    return ~{~a~^.concat(~}~:*~{~*~^)~};\n"
                            (map (lambda (eltno descriptor-name)
                                   (format "~a.toValue(~a[~d])" descriptor-name value eltno))
                                 (enumerate type*)
                                 descriptor-name*)))
                (printf "  }\n")))
            (printf "}\n\n")))
        (define (print-descriptor descriptor-id type)
          (define (byte-length n) (div (+ (integer-length n) 7) 8))
          (let ([descriptor-name (format-internal-binding unique-global-name descriptor-id)])
            (printf "const ~a = ~a;\n\n"
              descriptor-name
              (nanopass-case (Ltypescript Type) (de-alias type)
                [(tboolean ,src)
                 "__compactRuntime.CompactTypeBoolean"]
                [(tfield ,src ,ftype)
                 (nanopass-case (Ltypescript Field-Type) ftype
                   [(field-native) "__compactRuntime.CompactTypeField"]
                   [(field-scalar (curve-jubjub)) "__compactRuntime.CompactTypeField"]
                   [(field-base (curve-secp256k1))
                    "__compactRuntime.CompactTypeSecp256k1Base"]
                   [(field-scalar (curve-secp256k1))
                    "__compactRuntime.CompactTypeSecp256k1Scalar"])]
                [(tunsigned ,src ,nat)
                 (format "new __compactRuntime.CompactTypeUnsignedInteger(~dn, ~d)"
                   nat (max 1 (byte-length nat)))]
                [(tpoint ,src ,ctype)
                 (nanopass-case (Ltypescript Curve-Type) ctype
                   [(curve-jubjub) "__compactRuntime.CompactTypeJubjubPoint"]
                   [(curve-secp256k1) "__compactRuntime.CompactTypeSecp256k1Point"])]
                [(tbytes ,src ,len)
                 (format "new __compactRuntime.CompactTypeBytes(~d)" len)]
                [(topaque ,src ,opaque-type)
                 (case opaque-type
                   [("string") "__compactRuntime.CompactTypeOpaqueString"]
                   [("Uint8Array") "__compactRuntime.CompactTypeOpaqueUint8Array"]
                   ; FIXME: what should happen with other opaque types?
                   [else (source-errorf src "opaque type ~a is not supported" opaque-type)])]
                [(tvector ,src ,len ,type)
                 (format "new __compactRuntime.CompactTypeVector(~d, ~a)"
                         len
                         (type->descriptor-name type))]
                [(tcontract ,src ,contract-name (,elt-name* ,pure-dcl* (,type** ...) ,type*) ...)
                 (assert cannot-happen)]
                [(ttuple ,src ,type* ...)
                 (let ([tuple-name (format-internal-binding unique-global-name (make-temp-id src 'tuple))])
                   (print-tuple-class tuple-name type*)
                   (format "new ~a()" tuple-name))]
                [(tstruct ,src ,struct-name (,elt-name* ,type*) ...)
                 (let ([class-name (format-internal-binding unique-global-name (make-temp-id src struct-name))])
                   (print-struct-class class-name elt-name* type*)
                   (format "new ~a()" class-name))]
                [(tenum ,src ,enum-name ,elt-name ,elt-name* ...)
                 (let ([n (length elt-name*)])
                   (format "new __compactRuntime.CompactTypeEnum(~d, ~d)"
                     n (max 1 (byte-length n))))]
                [(tunknown) (assert cannot-happen)]
                [else (assert cannot-happen)]))))
        (for-each print-descriptor descriptor-id* type*))

      (module (print-contract-class)
        (define (ledger-initializers src state pl-array q*)
          (define (initialize-array pl-array stateValue q*)
            (cons*
              2 (format "let ~a = __compactRuntime.StateValue.newArray();" stateValue)
              (nanopass-case (Ltypescript Public-Ledger-Array) pl-array
                [(public-ledger-array ,pl-array-elt* ...)
                 (fold-right
                   (lambda (pl-array-elt q*)
                     (nanopass-case (Ltypescript Public-Ledger-Array-Element) pl-array-elt
                       [,pl-array
                        (let ([stateValue^ (format-internal-binding unique-local-name (make-temp-id src 'stateValue))])
                          (initialize-array pl-array stateValue^
                            (cons*
                              2 (format "~a = ~:*~a.arrayPush(~a);" stateValue stateValue^)
                              q*)))]
                       [,public-binding
                        (cons*
                          2 (format "~a = ~:*~a.arrayPush(__compactRuntime.StateValue.newNull());" stateValue)
                          q*)]))
                   q*
                   pl-array-elt*)])))
          (let ([stateValue (format-internal-binding unique-local-name (make-temp-id src 'stateValue))])
            (initialize-array pl-array stateValue
              (cons*
                2 (format "~a.data = new __compactRuntime.ChargedState(~a);" state stateValue)
                q*))))

        (define (ledger-reset-to-default src pl-array q*)
          (define (find-adt-op ledger-op adt-op*)
            (assert (find (lambda (adt-op)
                            (nanopass-case (Ltypescript ADT-Op) adt-op
                              [(,ledger-op^ ,op-class (,adt-name (,adt-formal* ,adt-arg*) ...) ((,var-name* ,type*) ...) ,type ,vm-code)
                               (eq? ledger-op^ ledger-op)]))
                          adt-op*)))
          (fold-right
            (lambda (public-binding q*)
              (nanopass-case (Ltypescript Public-Ledger-Binding) public-binding
                [(,src ,ledger-field-name (,path-index* ...) ,type)
                 (nanopass-case (Ltypescript Type) (de-alias type)
                   [(tadt ,src^ ,adt-name ([,adt-formal* ,adt-arg*] ...) ,vm-expr (,adt-op* ...) (,adt-rt-op* ...))
                    (cons*
                      2 (construct-query src path-index* adt-formal* adt-arg* (find-adt-op 'resetToDefault adt-op*) '()) ";"
                      q*)]
                   [else (assert cannot-happen)])]))
            q*
            (pl-array->public-bindings pl-array)))

        (module (argument-type-checks context-type-check result-type-check)
          (define (typeof type var)
            (let ([type (de-alias type)])
              (let ([type (subst-tcontract type)])
                (nanopass-case (Ltypescript Type) type
                  [(tboolean ,src) (format "typeof(~a) === 'boolean'" var)]
                  [(tfield ,src ,ftype)
                   (let ([field-name
                           (nanopass-case (Ltypescript Field-Type) ftype
                             [(field-native) "FIELD"]
                             [(field-scalar (curve-jubjub)) "JUBJUB_SCALAR"]
                             [(field-base (curve-secp256k1)) "SECP256K1_BASE"]
                             [(field-scalar (curve-secp256k1)) "SECP256K1_SCALAR"])])
                     (format "typeof(~a) === 'bigint' && ~:*~a >= 0 && ~:*~a <= __compactRuntime.MAX_~a"
                       var field-name))]
                  [(tunsigned ,src ,nat)
                   (format "typeof(~a) === 'bigint' && ~:*~a >= 0n && ~:*~a <= ~dn" var nat)]
                  [(tpoint ,src ,ctype)
                   ;; The tfield case above bounds a bare field element, but an
                   ;; unbounded check here would let the same bigint through as
                   ;; a coordinate, so bound these too - otherwise the
                   ;; failure surfaces inside CompactTypeSecp256k1Base.toValue,
                   ;; far from the call site. Curve membership and canonical
                   ;; identity points are still unchecked.
                   (let ([coordinate
                           (lambda (field bound)
                             (format "typeof(~a.~a) === 'bigint' && ~a.~a >= 0n && ~a.~a <= __compactRuntime.~a"
                               var field var field var field bound))])
                     (nanopass-case (Ltypescript Curve-Type) ctype
                       [(curve-jubjub)
                        (format "~a && ~a"
                          (coordinate "x" "MAX_FIELD")
                          (coordinate "y" "MAX_FIELD"))]
                       [(curve-secp256k1)
                        (format "~a && ~a && typeof(~a.identity) === 'boolean'"
                          (coordinate "x" "MAX_SECP256K1_BASE")
                          (coordinate "y" "MAX_SECP256K1_BASE")
                          var)]))]
                  [(tbytes ,src ,len)
                   (format "~a.buffer instanceof ArrayBuffer && ~:*~a.BYTES_PER_ELEMENT === 1 && ~:*~a.length === ~s" var len)]
                  [(topaque ,src ,opaque-type)
                   (case opaque-type
                     [("string") (format "typeof (~a) === 'string'" var)]
                     [("Uint8Array") (format "~a instanceof Uint8Array" var)]
                     [else (assertf cannot-happen
                             "cannot insert runtime type check for Opaque<'~a'>"
                             opaque-type)])]
                  [(tvector ,src ,len ,type)
                   (format "Array.isArray(~a) && ~:*~a.length === ~d && ~2:*~a.every((t) => ~*~a)"
                           var len (typeof type "t"))]
                  [(tcontract ,src ,contract-name (,elt-name* ,pure-dcl* (,type** ...) ,type*) ...)
                   (assert cannot-happen)]
                  [(ttuple ,src ,type* ...)
                   (format "Array.isArray(~a) && ~:*~a.length === ~d ~{ && ~a~}"
                     var
                     (length type*)
                     (map (lambda (eltno type) (typeof type (format "~a[~d]" var eltno)))
                          (enumerate type*)
                          type*))]
                  [(tstruct ,src ,struct-name (,elt-name* ,type*) ...)
                   ; ignoring struct-name, so we're getting structural typing.  also ignoring extra fields.
                   (format "typeof(~a) === 'object'~{ && ~a~}" var (map (lambda (elt-name type) (typeof type (format "~a.~s" var elt-name))) elt-name* type*))]
                  [(tenum ,src ,enum-name ,elt-name ,elt-name* ...)
                   (format "typeof(~a) === 'number' && ~:*~a >= 0 && ~:*~a <= ~d" var (length elt-name*))]
                  [(tunknown) (assert cannot-happen)]
                  [(tadt ,src ,adt-name ([,adt-formal* ,adt-arg*] ...) ,vm-expr (,adt-op* ...) (,adt-rt-op* ...))
                   (assert cannot-happen)]))))

          (define (argument-type-checks src what extra-arguments var-name* type* q*)
            (fold-right
              (lambda (var-name type i q*)
                (let ([arg-name (format-id-reference var-name)])
                  (let ([typeof-expr (typeof type arg-name)])
                    (if (equal? typeof-expr "true")
                        q*
                        (cons*
                          2
                          (make-Qconcat
                            "if (!("
                            typeof-expr
                            ")) {"
                            2 (compact-stdlib "typeError")
                            "("
                            ((make-Qsep ",")
                              (format "'~a'" what)
                              (let ([i (fx+ i 1)])
                                (if (= extra-arguments 0)
                                    (format "'argument ~d'" i)
                                    (format "'argument ~d (argument ~d as invoked from Typescript)'" i (fx+ i extra-arguments))))
                              (format "'~a'" (format-source-object src))
                              (format "'~a'" (format-type type))
                              arg-name)
                            ")"
                            0 "}")
                          q*)))))
              q*
              var-name*
              type*
              (enumerate var-name*)))

          (define (context-type-check src what var q*)
            (cons*
              2 (make-Qconcat
                  "if (!("
                  ; we don't insist on currentPrivateState being defined
                  (format "typeof(~a) === 'object' && ~:*~a.callContext.currentQueryContext != undefined" var)
                  ")) {"
                  2 (compact-stdlib "typeError")
                  "("
                  ((make-Qsep ",")
                   (format "'~a'" what)
                   "'argument 1 (as invoked from Typescript)'"
                   (format "'~a'" (format-source-object src))
                   "'CircuitContext'"
                   var)
                  ")"
                  0 "}")
              q*))

          (define (result-type-check src what type result q*)
            (let ([typeof-expr (typeof type result)])
              (if (equal? typeof-expr "true")
                  q*
                  (cons*
                    2
                    (make-Qconcat
                      "if (!("
                      typeof-expr
                      ")) {"
                      2 (compact-stdlib "typeError")
                      "("
                      ((make-Qsep ",")
                        (format "'~a'" what)
                        "'return value'"
                        (format "'~a'" (format-source-object src))
                        (format "'~a'" (format-type type))
                        result)
                      ")"
                      0 "}")
                    q*)))))

        (define (witness-checks witnesses xpelt* q*)
          (cons*
            2 (format "if (typeof(~a) !== 'object') {" witnesses)
            4 (format "throw new ~a('first (witnesses) argument to Contract constructor is not an object');"
                      (compact-stdlib "CompactError"))
            2 "}"
            (fold-right
              (lambda (xpelt q*)
                (XPelt-case xpelt
                  [(XPelt-witness src internal-id arg* type external-name)
                   (cons*
                     2 (format "if (typeof(~a.~a) !== 'function') {" witnesses external-name)
                     4 (format "throw new ~a('first (witnesses) argument to Contract constructor does not contain a function-valued field named ~a');"
                               (compact-stdlib "CompactError")
                               external-name)
                     2 "}"
                     q*)]
                  [else q*]))
              q*
              xpelt*)))

        (module (bind-args bind-args-with-context)
          (define (bind-arg-helper with-context? args)
            (lambda (q-name i q*)
              (cons* 2 "const " q-name (format " = ~a[~d];" args (if with-context? (+ i 1) i)) q*)))
          (define (bind-args args name* q*)
            (fold-right
              (bind-arg-helper #f args)
              q*
              name*
              (enumerate name*)))

          (define (bind-args-with-context ctxt args name* q*)
            (cons*
              2 "const " ctxt (format " = ~a[~d];" args 0)
              (fold-right
                (bind-arg-helper #t args)
              q*
              name*
              (enumerate name*)))))

        (define (build-exported-pure-circuits xpelt* uname*)
          (apply (make-Qsep ",")
            (fold-right
              (lambda (xpelt uname q*)
                (XPelt-case xpelt
                  [(XPelt-exported-circuit src internal-id arg* type stmt external-name* pure?)
                   (if pure?
                      (for-each
                        (lambda (external-name)
                          (with-local-unique-names
                            (let* ([args (format-internal-binding unique-local-name (make-temp-id src 'args))]
                                   [q-formal* (map make-Qformal! arg*)]
                                   [nargs (length arg*)])
                              (cons
                                (apply make-Qconcat/src src
                                  (make-Qconcat/src (id-src internal-id) external-name)
                                  (format ": (...~a) => {" args)
                                  2 (make-Qconcat
                                      (format "if (~a.length !== ~d) {" args nargs)
                                      2 (format "throw new ~a(`~a: expected ~d argument~:*~p (as invoked from Typescript), received ${~a.length}`);"
                                                (compact-stdlib "CompactError")
                                                (format "~a" external-name)
                                                nargs
                                                args)
                                      0 "}")
                                  (bind-args args q-formal*
                                    (argument-type-checks src external-name 0 (map arg->id arg*) (map arg->type arg*)
                                      (list
                                        2 "return _dummyContract." uname "(" (apply (make-Qsep ",") q-formal*) ");"
                                        0 "}"))))
                                q*))))
                        external-name*)
                     q*)]
                  [else q*]))
              '()
              xpelt*
              uname*)))

        (define (build-exported-circuits xpelt* uname*)
          (apply (make-Qsep ",")
            (fold-right
              (lambda (xpelt uname q*)
                (XPelt-case xpelt
                  [(XPelt-exported-circuit src internal-id arg* type stmt external-name* pure?)
                   (if (not pure?)
                     (with-local-unique-names
                       (let* ([args (format-internal-binding unique-local-name (make-temp-id src 'args))]
                              [contextOrig (format-internal-binding unique-local-name (make-temp-id src 'contextOrig))]
                              [result (format-internal-binding unique-local-name (make-temp-id src 'result))]
                              [descriptor-name* (map type->descriptor-name (map arg->type arg*))]
                              [descriptor-name? (type->maybe-descriptor-name type)]
                              [q-formal* (map make-Qformal! arg*)]
                              [nargs (fx+ (length arg*) 1)])
                         (append
                           (map (lambda (external-name)
                                  (apply make-Qconcat/src src
                                    (make-Qconcat/src (id-src internal-id) external-name)
                                    (format ": async (...~a) => {" args)
                                    2 (make-Qconcat
                                        (format "if (~a.length !== ~d) {" args nargs)
                                        2 (format "throw new ~a(`~a: expected ~d argument~:*~p (as invoked from Typescript), received ${~a.length}`);"
                                                  (compact-stdlib "CompactError")
                                                  (format "~a" external-name)
                                                  nargs
                                                  args)
                                        0 "}")
                                    (bind-args-with-context contextOrig args q-formal*
                                      (context-type-check src external-name contextOrig
                                        (argument-type-checks src external-name 1 (map arg->id arg*) (map arg->type arg*)
                                          (list
                                            2 (format "const context = __compactRuntime.copyCircuitContext(~a);" contextOrig)
                                            2 "const partialProofData = {"
                                            4 (make-Qconcat
                                                "input: {"
                                                2 ((make-Qsep ",")
                                                   (make-Qconcat
                                                     "value: "
                                                     (if (null? q-formal*)
                                                         "[]"
                                                         (let f ([descriptor-name* descriptor-name*]
                                                                 [q-formal* q-formal*])
                                                           (let ([descriptor-name (car descriptor-name*)]
                                                                 [q-formal (car q-formal*)]
                                                                 [descriptor-name* (cdr descriptor-name*)]
                                                                 [q-formal* (cdr q-formal*)])
                                                             (let ([q (make-Qconcat
                                                                        (format "~a.toValue(" descriptor-name)
                                                                        q-formal
                                                                        ")")])
                                                               (if (null? descriptor-name*)
                                                                   q
                                                                   (make-Qconcat
                                                                     q
                                                                     ".concat("
                                                                     (f descriptor-name* q-formal*)
                                                                     ")")))))))
                                                   (make-Qconcat
                                                     "alignment: "
                                                     (if (null? q-formal*)
                                                         "[]"
                                                         (let f ([descriptor-name* descriptor-name*]
                                                                 [q-formal* q-formal*])
                                                           (let ([descriptor-name (car descriptor-name*)]
                                                                 [q-formal (car q-formal*)]
                                                                 [descriptor-name* (cdr descriptor-name*)]
                                                                 [q-formal* (cdr q-formal*)])
                                                             (let ([q (format "~a.alignment()" descriptor-name)])
                                                               (if (null? descriptor-name*)
                                                                   q
                                                                   (make-Qconcat
                                                                     q
                                                                     ".concat("
                                                                     (f descriptor-name* q-formal*)
                                                                     ")"))))))))
                                                0 "},")
                                            4 "output: undefined,"
                                            4 "publicTranscript: [],"
                                            4 "privateTranscriptOutputs: [],"
                                            4 "innerProofs: []"
                                            2 "};"
                                            2 (format "const ~a = await this." result)
                                              uname "("
                                              (apply (make-Qsep ",") "context" "partialProofData" q-formal*)
                                              ");"
                                            2 "partialProofData.output = { "
                                              "value: " (if descriptor-name?
                                                            (format "~a.toValue(~a)" descriptor-name? result)
                                                            "[]")
                                              ", "
                                              "alignment: " (if descriptor-name?
                                                                (format "~a.alignment()" descriptor-name?)
                                                                "[]")
                                              " };"
                                            2 "__compactRuntime.finalizeCallProofData(context, partialProofData);"
                                            2 "return { "
                                              "result: " result ", "
                                              "context: " "context" ", "
                                              "gasCost: " "context.callContext.currentGasCost"
                                              " };"
                                            0 "}"))))))
                                external-name*)
                           q*)))
                     (with-local-unique-names
                       (let ([args (format-internal-binding unique-local-name (make-temp-id src 'args))])
                         (append
                           ;; Async for a uniform call shape, though nothing here awaits.
                           (map (lambda (external-name)
                                  (make-Qconcat/src src
                                    "async "
                                    (make-Qconcat/src (id-src internal-id) external-name)
                                    (format "(context, ...~a) {" args)
                                    2 (format "return { result: pureCircuits.~a(...~a), context };" external-name args)
                                    0 "}"))
                             external-name*)
                           q*))))]
                    [else q*]))
              '()
              xpelt*
              uname*)))

        (define (get-pure&impure-circuit-names xpelt*)
          (if (null? xpelt*)
              (values '() '())
              (let-values ([(pure-name* impure-name*) (get-pure&impure-circuit-names (cdr xpelt*))])
                (XPelt-case (car xpelt*)
                  [(XPelt-exported-circuit src internal-id arg* type stmt external-name* pure?)
                   (if pure?
                       (values
                         (append external-name* pure-name*)
                         impure-name*)
                       (values
                         pure-name*
                         (append external-name* impure-name*)))]
                  [else (values pure-name* impure-name*)]))))

        (define (get-provable-circuit-names xpelt*)
          (fold-right
            (lambda (xpelt rest)
              (XPelt-case xpelt
                [(XPelt-exported-circuit src internal-id arg* type stmt external-name* pure?)
                 (append (filter
                           (lambda (name) (memq (string->symbol name) (proof-circuit-names)))
                           external-name*)
                         rest)]
                [else rest]))
            '()
            xpelt*))

        (define (get-witness-names xpelt*)
          (fold-right
            (lambda (xpelt witness-name*)
              (XPelt-case xpelt
                [(XPelt-witness src internal-id arg* type external-name)
                 (cons external-name witness-name*)]
                [else witness-name*]))
            '()
            xpelt*))

        (define (build-circuit-name-object prefix name*)
          (apply make-Qconcat
            (format "this.~a = {" prefix)
            (if (null? name*)
                (list "};")
                (let f ([name* name*])
                  (let ([name (car name*)]
                        [name* (cdr name*)])
                    (let ([q (format "~a: this.circuits.~:*~a" name)])
                      (if (null? name*)
                          (list 2 q 0 "};")
                          (cons* 2 q "," (f name*)))))))))

        (define (print-contract-constructor xpelt0* uname* impure-name* provable-name*)
          (let loop ([xpelt* xpelt0*])
            (assert (not (null? xpelt*)))
            (XPelt-case (car xpelt*)
              [(XPelt-public-ledger pl-array lconstructor external-names)
               (nanopass-case (Ltypescript Ledger-Constructor) lconstructor
                 [(constructor ,src (,arg* ...) ,stmt)
                  (with-local-unique-names
                    (print-Q 2
                      (let* ([witnesses (format-internal-binding unique-local-name (make-temp-id src 'witnesses))]
                             [args (format-internal-binding unique-local-name (make-temp-id src 'args))]
                             [nargs 1])
                        (apply make-Qconcat/src src
                               (format "constructor(...~a) {" args)
                               2 (format "if (~a.length !== ~d) {" args nargs)
                               4 (format "throw new __compactRuntime.CompactError(`Contract constructor: expected ~d argument~:*~p, received ${~a.length}`);" nargs args)
                               2 "}"
                               (bind-args args (list witnesses)
                                 (witness-checks witnesses xpelt0*
                                   (list
                                     2 (format "this.witnesses = ~a;" witnesses)
                                     2 "this.circuits = {"
                                     4 (build-exported-circuits xpelt0* uname*)
                                     2 "};"
                                     2 (build-circuit-name-object "impureCircuits" impure-name*)
                                     2 (build-circuit-name-object "provableCircuits" provable-name*)
                                     0 "}")))))))
                  (newline)])]
              [else (loop (cdr xpelt*))])))

        (define (print-contract-ledger src xpelt0* uname*)
          (with-local-unique-names
            (define (ledger-field-Q src path-elt* adt-arg* all-op*)
              (apply (make-Qsep ",")
                (map (lambda (op)
                       (with-local-unique-names
                         (let-values ([(var-name* type*) (op-args op)])
                           (let ([formal* (map (lambda (var-name) (format-internal-binding unique-local-name var-name)) var-name*)]
                                 [args (format-internal-binding unique-local-name (make-temp-id src 'args))]
                                 [nargs (length var-name*)]
                                 [name (op-name op)])
                             (apply make-Qconcat
                               (maybe-iterator name)
                               (format "(...~a) {" args)
                               2 (format "if (~a.length !== ~d) {" args nargs)
                               4 (format "throw new __compactRuntime.CompactError(`~a: expected ~d argument~:*~p, received ${~a.length}`);" name nargs args)
                               2 "}"
                               (bind-args args formal*
                                 (argument-type-checks src name 0 var-name* type*
                                   (list
                                     2 (adt-op-body-Q src op path-elt* formal* adt-arg*)
                                     0 "}"))))))))
                     (remp (lambda (op)
                             ; run-time ops like iterators that expect to be supplied a descriptor
                             ; for use in decoding values.  we don't have a decoder for adt values, and if we did,
                             ; it's not clear what they would do with an adt value anyway.  so we just don't
                             ; generate such runtime ops.
                             (and (Ltypescript-ADT-Runtime-Op? op)
                                  (ormap (lambda (adt-arg)
                                           (nanopass-case (Ltypescript Public-Ledger-ADT-Arg) adt-arg
                                             [,type (public-adt? type)]
                                             [else #f]))
                                         adt-arg*)))
                           all-op*))))
            (define (adt-op-body-Q src adt-op path-elt* formal* adt-arg*)
              (define (path-chain-Q path-elt*)
                (apply make-Qconcat
                       (map (lambda (path-elt)
                              (nanopass-case (Ltypescript Path-Element) path-elt
                                [,path-index (format ".asArray()[~d]" path-index)]
                                [(,src ,type ,expr) (make-Qconcat
                                                      ".asMap().get("
                                                      (construct-typed-value
                                                        (type->descriptor-name type)
                                                        (Expr expr (precedence add1 comma) #f))
                                                      ")")]))
                            path-elt*)))
              (if (Ltypescript-ADT-Op? adt-op)
                  (nanopass-case (Ltypescript ADT-Op) adt-op
                    [(,ledger-op ,op-class (,adt-name (,adt-formal* ,adt-arg*) ...) ((,var-name* ,type*) ...) ,type ,vm-code)
                     (if (public-adt? type)
                         (begin
                           (assert (and (eq? ledger-op 'lookup)
                                        (fx= (length type*) 1)
                                        (not (public-adt? (car type*)))))
                           (let ([path-elt* (append path-elt*
                                                    (list (with-output-language (Ltypescript Path-Element)
                                                            `(,src ,(car type*) (var-ref ,src ,(car var-name*))))))])
                             (make-Qconcat
                               "if (state"
                               (path-chain-Q path-elt*)
                               " === undefined) {"
                               2 "throw new __compactRuntime.CompactError("
                               (format "`Map value undefined for ${~a}`" (format-id-reference (car var-name*)))
                               ");"
                               0 "}"
                               0 "return {"
                               2 (nanopass-case (Ltypescript Type) (de-alias type)
                                   [(tadt ,src ,adt-name ([,adt-formal* ,adt-arg*] ...) ,vm-expr (,adt-op* ...) (,adt-rt-op* ...))
                                    (let* ([all-op* (filter is-runtime-op? (append adt-op* adt-rt-op*))]
                                           [all-op* (cond [(has-read? all-op*) => list] [else all-op*])])
                                      (ledger-field-Q src path-elt* adt-arg* all-op*))])
                               0 "}")))
                         (let ([q (construct-query src path-elt* adt-formal* adt-arg* adt-op formal*)])
                           (let ([descriptor-name? (and (eq? op-class 'read)
                                                        (type->maybe-descriptor-name type))])
                             (if descriptor-name?
                                 (make-Qconcat
                                   "return "
                                   descriptor-name?
                                   ".fromValue("
                                   q
                                   ".value);")
                                 ; at present, this shouldn't happen.  we don't include side-effecting
                                 ; operations in ledger(state) return value, so all operations should
                                 ; return a value that needs to be decoded
                                 q))))])
                  (nanopass-case (Ltypescript ADT-Runtime-Op) adt-op
                    [(,ledger-op (,arg* ...) ,result-type ,runtime-code)
                     (let ([self (format-internal-binding unique-local-name (make-temp-id src 'self))])
                       (make-Qconcat
                         (format "const ~a = state" self)
                         (path-chain-Q path-elt*)
                         ";"
                         0 "return "
                         (apply make-Qconcat
                           (apply runtime-code
                                  "__compactRuntime."
                                  self
                                  (append
                                    formal*
                                    (map (lambda (adt-arg)
                                           (nanopass-case (Ltypescript Public-Ledger-ADT-Arg) adt-arg
                                             [,nat (number->string nat)]
                                             [,type
                                              (assert (not (public-adt? type)))
                                              (type->descriptor-name type)]))
                                         adt-arg*))))
                         ";"))])))
            (define (op-args adt-op)
              (if (Ltypescript-ADT-Op? adt-op)
                  (nanopass-case (Ltypescript ADT-Op) adt-op
                    [(,ledger-op ,op-class (,adt-name (,adt-formal* ,adt-arg*) ...) ((,var-name* ,type*) ...) ,type ,vm-code)
                     (values var-name* type*)])
                  (nanopass-case (Ltypescript ADT-Runtime-Op) adt-op
                    [(,ledger-op ((,var-name* ,type*) ...) ,result-type ,runtime-code)
                     (values var-name* type*)])))
            (let loop ([xpelt* xpelt0*])
              (assert (not (null? xpelt*)))
              (XPelt-case (car xpelt*)
                [(XPelt-public-ledger pl-array lconstructor external-names)
                 (print-Q 0
                    (make-Qconcat
                      "export function ledger(stateOrChargedState) {"
                      2 "const state = stateOrChargedState instanceof __compactRuntime.StateValue ? stateOrChargedState : stateOrChargedState.state;"
                      2 "const chargedState = stateOrChargedState instanceof __compactRuntime.StateValue ? new __compactRuntime.ChargedState(stateOrChargedState) : stateOrChargedState;"
                      2 "const context = {"
                      4 "callContext: { currentQueryContext: new __compactRuntime.QueryContext(chargedState, __compactRuntime.dummyContractAddress()), currentGasCost: __compactRuntime.emptyRunningCost() },"
                      4 "costModel: __compactRuntime.CostModel.initialCostModel()"
                      2 "};"
                      2 "const partialProofData = {"
                      4 "input: { value: [], alignment: [] },"
                      4 "output: undefined,"
                      4 "publicTranscript: [],"
                      4 "privateTranscriptOutputs: [],"
                      4 "innerProofs: []"
                      2 "};"
                      2 "return {"
                      4 (apply (make-Qsep ",")
                          (fold-right
                            (lambda (binding q*)
                              (nanopass-case (Ltypescript Public-Ledger-Binding) binding
                                [(,src ,ledger-field-name (,path-index* ...) ,type)
                                 (nanopass-case (Ltypescript Type) (de-alias type)
                                   [(tadt ,src^ ,adt-name ([,adt-formal* ,adt-arg*] ...) ,vm-expr (,adt-op* ...) (,adt-rt-op* ...))
                                    (fold-right
                                      (lambda (export-name q*)
                                        (cons
                                          (let* ([all-op* (filter is-runtime-op? (append adt-op* adt-rt-op*))]
                                                 [read-op (has-read? all-op*)])
                                            (if read-op
                                                (make-Qconcat/src src
                                                                  "get "
                                                                  export-name
                                                                  "() {"
                                                                  2 (adt-op-body-Q src read-op path-index* '() adt-arg*)
                                                                  0 "}")
                                                (make-Qconcat/src src
                                                                  export-name
                                                                  ": {"
                                                                  2 (ledger-field-Q src path-index* adt-arg* all-op*)
                                                                  0 "}")))
                                          q*))
                                      q*
                                      (external-names ledger-field-name))]
                                   [else (assertf cannot-happen "expected adt type, received ~a" type)])]))
                            '()
                            (filter
                              exported-public-binding?
                              (pl-array->public-bindings pl-array))))
                      2 "};"
                      0 "}"))
                 (newline)]
                [else (loop (cdr xpelt*))]))))

        (define (set-operations state xpelt* q*)
          (fold-right
            (lambda (xpelt q*)
              (XPelt-case xpelt
                [(XPelt-exported-circuit src internal-id arg* type stmt external-name* pure?)
                 (fold-right
                   (lambda (external-name q*)
                     (if (memq (string->symbol external-name) (proof-circuit-names))
                         (cons*
                           2 (format
                               "~a.setOperation('~a', new __compactRuntime.ContractOperation());"
                               state
                               external-name)
                           q*)
                         q*))
                   q*
                   external-name*)]
                [else q*]))
            q*
            xpelt*))

        (define (print-contract-initializer xpelt0* uname*)
          (let loop ([xpelt* xpelt0*])
            (assert (not (null? xpelt*)))
            (XPelt-case (car xpelt*)
              [(XPelt-public-ledger pl-array lconstructor external-names)
               (nanopass-case (Ltypescript Ledger-Constructor) lconstructor
                 [(constructor ,src (,arg* ...) ,stmt)
                  (with-local-unique-names
                    (print-Q 2
                      (let* ([args (format-internal-binding unique-local-name (make-temp-id src 'args))]
                             [constructorContext (format-internal-binding unique-local-name (make-temp-id src 'constructorContext))]
                             [state (format-internal-binding unique-local-name (make-temp-id src 'state))]
                             [q-formal* (map make-Qformal! arg*)]
                             [nargs (fx+ (length arg*) 1)])
                        (define (maybe-add-private-state-check k)
                          (if (null? (get-witness-names xpelt0*))
                              k
                              (cons*
                                2 (format "if (!('initialPrivateState' in ~a)) {" constructorContext)
                                4 "throw new __compactRuntime.CompactError(`Contract state constructor: expected 'initialPrivateState' in argument 1 (as invoked from Typescript)`);"
                                2 "}"
                                k)))
                        (apply make-Qconcat/src src
                               (format "async initialState(...~a) {" args)
                               2 (format "if (~a.length !== ~d) {" args nargs)
                               4 (format "throw new __compactRuntime.CompactError(`Contract state constructor: expected ~d argument~:*~p (as invoked from Typescript), received ${~a.length}`);" nargs args)
                               2 "}"
                               (bind-args args (cons constructorContext q-formal*)
                                 (cons*
                                   2 (format "if (typeof(~a) !== 'object') {" constructorContext)
                                   4 "throw new __compactRuntime.CompactError(`Contract state constructor: expected 'constructorContext' in argument 1 (as invoked from Typescript) to be an object`);"
                                   2 "}"
                                   (maybe-add-private-state-check
                                     (cons*
                                       2 (format "if (!('initialZswapLocalState' in ~a)) {" constructorContext)
                                       4 "throw new __compactRuntime.CompactError(`Contract state constructor: expected 'initialZswapLocalState' in argument 1 (as invoked from Typescript)`);"
                                       2 "}"
                                       2 (format "if (typeof(~a.initialZswapLocalState) !== 'object') {" constructorContext)
                                       4 "throw new __compactRuntime.CompactError(`Contract state constructor: expected 'initialZswapLocalState' in argument 1 (as invoked from Typescript) to be an object`);"
                                       2 "}"
                                       (argument-type-checks src "Contract state constructor" 1 (map arg->id arg*) (map arg->type arg*)
                                         (cons*
                                           2 (format "const ~a = new __compactRuntime.ContractState();" state)
                                           (ledger-initializers src state pl-array
                                             (set-operations state xpelt0*
                                               (cons*
                                                 2 (format "const context = __compactRuntime.createCircuitContext({circuitId: 'constructor', contractAddress: __compactRuntime.dummyContractAddress(), coinPublicKeyOrZswapState: ~a.initialZswapLocalState.coinPublicKey, contractState: ~a.data, privateState: ~a.initialPrivateState});" constructorContext state constructorContext)
                                                 2 "const partialProofData = {"
                                                 4 "input: { value: [], alignment: [] },"
                                                 4 "output: undefined,"
                                                 4 "publicTranscript: [],"
                                                 4 "privateTranscriptOutputs: [],"
                                                 4 "innerProofs: []"
                                                 2 "};"
                                                 (ledger-reset-to-default src pl-array
                                                   (list
                                                     2 (Stmt stmt #f #f)
                                                     2 (format "~a.data = new __compactRuntime.ChargedState(context.callContext.currentQueryContext.state.state);" state)
                                                     2 "return {"
                                                     4 (format "currentContractState: ~a," state)
                                                     4 "currentPrivateState: context.callContext.currentPrivateState,"
                                                     4 "currentZswapLocalState: context.callContext.currentZswapLocalState"
                                                     2 "}"
                                                     0 "}")))))))))))))))
                  (newline)])]
              [else (loop (cdr xpelt*))])))

        (define (print-unexported-circuit xpelt uname)
          (define (print-local-circuit src internal-id arg* stmt pure?)
            (with-local-unique-names
              (print-Q 2
                (let ([q-formal* (map make-Qformal! arg*)])
                  (make-Qconcat/src src
                    (make-Qconcat
                      (make-Qconcat/src (id-src internal-id)
                        (if pure? (format "~a" uname) (format "async ~a" uname)))
                      "("
                      (make-Qargs pure? q-formal*)
                      ")"
                      0 "{")
                    2 (Stmt stmt #t pure?)
                    0 "}"))))
            (newline))
          (XPelt-case xpelt
            [(XPelt-exported-circuit src internal-id arg* type stmt external-name* pure?)
             (print-local-circuit src internal-id arg* stmt pure?)]
            [(XPelt-internal-circuit src internal-id arg* type stmt pure?)
             (print-local-circuit src internal-id arg* stmt pure?)]
            [(XPelt-witness src internal-id arg* type external-name)
             (print-external-witness src internal-id uname arg* type external-name)]
            [(Xpelt-native-circuit src internal-id native-entry arg* type external-name pure?)
             (print-external-circuit src internal-id native-entry uname arg* type external-name pure?)]
            [else (void)]))

        (define (print-external-circuit src internal-id native-entry uname arg* type external-name pure?)
          (with-local-unique-names
            (let ([q-formal* (map make-Qformal! arg*)]
                  [result (format-internal-binding unique-local-name (make-temp-id src 'result))])
              (define (maybe-add-trascript-push q*)
                (if (eq? (native-entry-class native-entry) 'witness)
                    (let ([descriptor-name? (type->maybe-descriptor-name type)])
                      (cons*
                        2 "partialProofData.privateTranscriptOutputs.push({"
                        4 "value: " (if descriptor-name?
                                        (format "~a.toValue(~a)" descriptor-name? result)
                                        "[]")
                        ","
                        4 "alignment: " (if descriptor-name?
                                            (format "~a.alignment()" descriptor-name?)
                                            "[]")
                        2 "});"
                        q*))
                    q*))
              (print-Q 2
                (apply make-Qconcat/src src
                  (make-Qconcat
                    (make-Qconcat/src (id-src internal-id) (format "~a" uname))
                    "("
                    (make-Qargs pure? q-formal*)
                    ")"
                    0 "{")
                  2 (format "const ~a = " result)
                  (make-Qconcat
                    (native-entry-function native-entry)
                    "("
                    (apply (make-Qsep ",")
                           (fold-right
                             (let ([ht (make-hashtable symbol-hash eq?)])
                               (lambda (maybe-type-param type q*)
                                 (if (and maybe-type-param (not (hashtable-contains? ht maybe-type-param)))
                                     (begin
                                       (hashtable-set! ht maybe-type-param #t)
                                       (cons
                                         (type->descriptor-name type)
                                         q*))
                                     q*)))
                             (let ([arg-q* (map (lambda (arg)
                                                  (let ([var-name (arg->id arg)])
                                                    (make-Qconcat/src
                                                      (id-src var-name)
                                                      (format-id-reference var-name))))
                                                arg*)])
                               (if (eq? (native-entry-class native-entry) 'witness)
                                   (cons "context" arg-q*)
                                   arg-q*))
                             (native-entry-maybe-type-param* native-entry)
                             (append
                               (map arg->type arg*)
                               (list type))))
                    ")")
                  ";"
                  (maybe-add-trascript-push
                    (list
                      2 "return " result ";"
                      0 "}"))))))
          (newline))

        (define (print-external-witness src internal-id uname arg* type external-name)
          (with-local-unique-names
            (let ([result (format-internal-binding unique-local-name (make-temp-id src 'result))]
                  [witnessContext (format-internal-binding unique-local-name (make-temp-id src 'witnessContext))]
                  [nextPrivateState (format-internal-binding unique-local-name (make-temp-id src 'nextPrivateState))]
                  [descriptor-name? (type->maybe-descriptor-name type)])
              (print-Q 2
                (let ([q-formal* (map make-Qformal! arg*)])
                  (apply make-Qconcat/src src
                    (make-Qconcat
                      (make-Qconcat/src (id-src internal-id) (format "~a" uname))
                      "("
                      (make-Qargs #f q-formal*)
                      ")"
                      0 "{")
                    2 (format "const ~a = __compactRuntime.createWitnessContext(ledger(context.callContext.currentQueryContext.state), context.callContext.currentPrivateState, context.callContext.currentQueryContext.address);" witnessContext)
                    2 (format "const [~a, ~a] = " nextPrivateState result)
                    "this.witnesses."
                    (make-Qconcat/src (id-src internal-id) external-name)
                    "("
                    (apply (make-Qsep ",") witnessContext q-formal*)
                    ");"
                    2 (format "context.callContext.currentPrivateState = ~a;" nextPrivateState)
                    (result-type-check src external-name type result
                      (list
                        2 "partialProofData.privateTranscriptOutputs.push({"
                        4 "value: " (if descriptor-name?
                                        (format "~a.toValue(~a)" descriptor-name? result)
                                        "[]")
                          ","
                        4 "alignment: " (if descriptor-name?
                                            (format "~a.alignment()" descriptor-name?)
                                            "[]")
                        2 "});"
                        2 "return "
                        result
                        ";"
                        0 "}")))))
              (newline))))

        (define (print-contract-class src xpelt* uname*)
          (with-local-unique-names
            (demand-unique-local-name! "this")
            (demand-unique-local-name! "context")
            (demand-unique-local-name! "partialProofData")
            (let-values ([(pure-name* impure-name*) (get-pure&impure-circuit-names xpelt*)])
              (let ([provable-name* (get-provable-circuit-names xpelt*)])
                (display-string "export class Contract {\n")
                (display-string "  witnesses;\n")
                (fluid-let ([helper* '()])
                  (print-contract-constructor xpelt* uname* impure-name* provable-name*)
                  (print-contract-initializer xpelt* uname*)
                  (for-each print-unexported-circuit xpelt* uname*)
                  (for-each display-string (reverse helper*)))
                (display-string "}\n")
                (print-contract-ledger src xpelt* uname*)
                (display-string "const _emptyContext = {\n")
                (display-string "  callContext: { currentQueryContext: new __compactRuntime.QueryContext(new __compactRuntime.ContractState().data, __compactRuntime.dummyContractAddress()), currentGasCost: __compactRuntime.emptyRunningCost() }\n")
                (display-string "};\n")
                (print-Q 0
                  (make-Qconcat
                    "const _dummyContract = new Contract({"
                    2 (apply (make-Qsep ",")
                             (map (lambda (witness-name)
                                    (format "~a: (...args) => undefined" witness-name))
                                  (get-witness-names xpelt*)))
                    0 "});"))
                (newline)
                (print-Q 0
                  (apply make-Qconcat
                    "export const pureCircuits = {"
                    (if (null? pure-name*)
                        (list "};")
                        (list 2 (build-exported-pure-circuits xpelt* uname*) 0 "};"))))
                (newline))))))

      (define (print-exported-types xpelt*)
        (for-each
          (lambda (xpelt)
            (XPelt-case xpelt
              [(XPelt-type-definition src type-name export-name tvar-name* type)
               (nanopass-case (Ltypescript Type) type
                 [(tenum ,src ,enum-name ,elt-name ,elt-name* ...)
                  (printf "export var ~a;\n" export-name)
                  (printf "(function (~a) {\n" export-name)
                  (let ([elt-name* (cons elt-name elt-name*)])
                    (for-each
                      (lambda (elt-name i)
                        (printf "  ~a[~:*~a['~a'] = ~d] = '~2:*~a';\n" export-name elt-name i))
                      elt-name*
                      (enumerate elt-name*)))
                  (printf "})(~a || (~:*~a = {}));\n\n" export-name)]
                 [else (void)])]
              [else (void)]))
          xpelt*))

      (define (comma-separated s*)
        (if (null? s*) "" (format "~a~{, ~a~}" (car s*) (cdr s*))))

      ;; A non-empty sequence whose elements all encode alike is a Vector;
      ;; anything else is a Tuple.  A zero-length sequence is a Tuple, because
      ;; Vector<0,T> has no recoverable element type.
      (define (format-sequence-type s*)
        (cond
          [(null? s*) "{tag: 'Tuple', types: []}"]
          [(andmap (lambda (s) (string=? s (car s*))) (cdr s*))
           (format "{tag: 'Vector', length: ~d, type: ~a}" (length s*) (car s*))]
          [else (format "{tag: 'Tuple', types: [~a]}" (comma-separated s*))]))

      (define (format-interface-descriptor elt-name* pure-dcl* type** type*)
        (format "{~a}"
          (comma-separated
            (map (lambda (elt-name pure-dcl arg-type* result-type)
                   (format "~a: {pure: ~a, argumentTypes: [~a], resultType: ~a}"
                     (format-javascript-string (symbol->string elt-name))
                     (if pure-dcl "true" "false")
                     (comma-separated (map format-compact-type arg-type*))
                     (format-compact-type result-type)))
                 elt-name* pure-dcl* type** type*))))

      (define (format-compact-type type)
        (nanopass-case (Ltypescript Type) type
          [(tboolean ,src) "{tag: 'Boolean'}"]
          [(tfield ,src ,ftype)
           (format "{tag: ~a}" (format-javascript-string (format-field-type ftype)))]
          [(tpoint ,src ,ctype)
           (format "{tag: ~a}" (format-javascript-string (format-point-type ctype)))]
          [(tunsigned ,src ,nat)
           (format "{tag: 'Uint', maxval: ~a}"
             (format-javascript-string (number->string nat)))]
          [(tbytes ,src ,len) (format "{tag: 'Bytes', length: ~d}" len)]
          [(topaque ,src ,opaque-type)
           (format "{tag: 'Opaque', tsType: ~a}"
             (format-javascript-string opaque-type))]
          [(tvector ,src ,len ,type)
           (format-sequence-type (make-list len (format-compact-type type)))]
          [(ttuple ,src ,type* ...)
           (format-sequence-type (map format-compact-type type*))]
          [(tstruct ,src ,struct-name (,elt-name* ,type*) ...)
           (format "{tag: 'Struct', name: ~a, elements: [~a]}"
             (format-javascript-string (symbol->string struct-name))
             (comma-separated
               (map (lambda (elt-name type)
                      (format "{name: ~a, type: ~a}"
                        (format-javascript-string (symbol->string elt-name))
                        (format-compact-type type)))
                    elt-name* type*)))]
          [(tenum ,src ,enum-name ,elt-name ,elt-name* ...)
           (format "{tag: 'Enum', name: ~a, elements: [~a]}"
             (format-javascript-string (symbol->string enum-name))
             (comma-separated
               (map (lambda (n) (format-javascript-string (symbol->string n)))
                    (cons elt-name elt-name*))))]
          ;; A transparent alias is erased; only a `new type` survives as a node.
          [(talias ,src ,nominal? ,type-name ,type)
           (if nominal?
               (format "{tag: 'Alias', name: ~a, type: ~a}"
                 (format-javascript-string (symbol->string type-name))
                 (format-compact-type type))
               (format-compact-type type))]
          [(tcontract ,src ,contract-name (,elt-name* ,pure-dcl* (,type** ...) ,type*) ...)
           (format "{tag: 'Contract', name: ~a, circuits: ~a}"
             (format-javascript-string (symbol->string contract-name))
             (format-interface-descriptor elt-name* pure-dcl* type** type*))]
          [(tadt ,src ,adt-name ([,adt-formal* ,adt-arg*] ...) ,vm-expr (,adt-op* ...) (,adt-rt-op* ...))
           (assert cannot-happen)]
          [,tvar-name (assert cannot-happen)]
          [(tunknown) (assert cannot-happen)]))

      (define (format-argument-type arg)
        (nanopass-case (Ltypescript Argument) arg
          [(,var-name ,type) (format-compact-type type)]))

      (define (print-table name entry*)
        (if (null? entry*)
            (printf "export const ~a = {};\n" name)
            (begin
              (printf "export const ~a = {\n" name)
              (for-each (lambda (entry) (printf "  ~a,\n" entry)) entry*)
              (display-string "};\n")))
        (newline))

      ;; One entry per external name, so a circuit exported under several fans out, as expectedVk
      ;; and pureCircuits already do.
      (define (print-circuit-signatures xpelt*)
        (print-table "circuitSignatures"
          (fold-right
            (lambda (xpelt entry*)
              (XPelt-case xpelt
                [(XPelt-exported-circuit src internal-id arg* type stmt external-name* pure?)
                 (fold-right
                   (lambda (external-name entry*)
                     (cons
                       (format "~a: {pure: ~a, provable: ~a, argumentTypes: [~a], resultType: ~a}"
                         (format-javascript-string external-name)
                         (if pure? "true" "false")
                         (if (memq (string->symbol external-name) (proof-circuit-names))
                             "true"
                             "false")
                         (comma-separated (map format-argument-type arg*))
                         (format-compact-type type))
                       entry*))
                   entry*
                   external-name*)]
                [else entry*]))
            '()
            xpelt*)))

      ;; contract-type* holds exactly the contract types a call is made on.
      (define (print-declared-interfaces contract-type*)
        (print-table "declaredInterfaces"
          (map
            (lambda (contract-type)
              (nanopass-case (Ltypescript Contract-Type) contract-type
                [(tcontract ,src ,contract-name (,elt-name* ,pure-dcl* (,type** ...) ,type*) ...)
                 (format "~a: ~a"
                   (format-javascript-string (symbol->string contract-name))
                   (format-interface-descriptor elt-name* pure-dcl* type** type*))]))
            contract-type*)))

      ;; The per-circuit fingerprints computed in passes.ss after key generation. Empty for a build
      ;; that generates no keys, such as --skip-zk.
      (define (print-expected-vk)
        (let ([vk* (verifier-key-hashes)])
          (if (null? vk*)
              (display-string "export const expectedVk = {};\n")
              (begin
                (display-string "export const expectedVk = {\n")
                (for-each
                  (lambda (pair)
                    (printf "  ~a: ~a,\n"
                      (format-javascript-string (car pair))
                      (format-javascript-string (cdr pair))))
                  vk*)
                (display-string "};\n")))
          (newline)))

      (define (print-contract-footer)
        (display-string "//# sourceMappingURL=index.js.map\n"))

      (define (print-contract.js src contract-type* descriptor-id* type* xpelt* uname*)
        (parameterize ([current-output-port (get-target-port 'contract.js)])
          (fluid-let ([sourcemap-tracker (make-sourcemap-tracker)])
            (print-contract-header)
            (print-exported-types xpelt*)
            (print-contract-descriptors src descriptor-id* type*)
            (print-contract-class src xpelt* uname*)
            (print-expected-vk)
            (print-circuit-signatures xpelt*)
            (print-declared-interfaces contract-type*)
            (print-contract-footer)
            (record-sourcemap-eof! sourcemap-tracker (port-position (current-output-port)))
            (display-sourcemap sourcemap-tracker (get-target-port 'contract.js.map))))))

    (define (format-javascript-string s)
      (define quote-seen #f)
      ((lambda (s) (format "~c~a~2:*~c" (if (eqv? quote-seen #\') #\" #\') s))
       (with-output-to-string
         (lambda ()
           (let ([n (string-length s)])
             (do ([i 0 (fx+ i 1)])
                 ((fx= i n))
               (let ([c (string-ref s i)])
                 (cond
                   [(or (char=? c #\") (char=? c #\'))
                    (cond
                      [(not quote-seen) (set! quote-seen c)]
                      [(eqv? c quote-seen)]
                      [else (write-char #\\)])
                    (write-char c)]
                   [(char=? c #\\) (display-string "\\\\")]
                   [(or (char<=? #\x20 c #\x7e)
                        (and (char>=? c #\x80)
                             (not (char=? c #\nel))
                             (not (char=? c #\ls))))
                    (write-char c)]
                   [(char=? c #\newline) (display-string "\\n")]
                   [(char=? c #\return) (display-string "\\r")]
                   [(char=? c #\nul) (display-string "\\0")]
                   [(char=? c #\backspace) (display-string "\\b")]
                   [(char=? c #\page) (display-string "\\f")]
                   [(char=? c #\tab) (display-string "\\t")]
                   [(char=? c #\vtab) (display-string "\\v")]
                   [(char<=? c #\xFF) (printf "\\x~2,'0x" (char->integer c))]
                   [else (printf "\\u~4,'0x" (char->integer c))]))))))))


    ;; The Q mechanism defined below is used for pretty-printing of generated javascript.
    ;; "Q" doesn't stand for anything; it's just an arbitrary flag identifiying entites
    ;; related to pretty-printing.

    ;; A Q is a string, a fixnum, or a Q record.
    ;;  string:   used to signify what will actually be printed
    ;;  fixnum:   used for spacing and indentation
    ;;  Qconcat:  formed from a sequence of Qs and used for grouping
    ;;  Qsegment: formed from a source object and a flag saying whether
    ;;            it marks the start or end of a source region and used
    ;;            to emit source-map segments to a source-map file

    ;; Every Q has a size:
    ;;   string:   the string's length
    ;;   fixnum:   1
    ;;   Qconcat:  sum of the sizes of its consituents
    ;;   Qsegment: 0

    ;; When a Q is printed, the sum of its size and the current indentation is
    ;; compared with the target line length.

    ;; If there is enough room, the entire Q is printed on one line as follows:
    ;;   string:   print the contents of the string
    ;;   fixnum:   print a space
    ;;   Qconcat:  print the components in sequence
    ;;   Qsegment: emit source-map segment to the map file

    ;; If there isn't enough room, the Q is potentially printed on multiple lines:
    ;;   string:   print the contents of the string
    ;;             (even if it runs beyond the end of the line)
    ;;   fixnum:   print a newline followed by n spaces, where n is the sum of the
    ;;             fixnum and the starting column of the closest enclosing Qconcat.
    ;;             should be used only in a sequence (i.e., within a Qconcat), where
    ;;             it increases the current indentation for the next item in the
    ;;             sequence only
    ;;   Qconcat:  recur on the constituents in sequence at the current indentation
    ;;   Qsegment: emit source-map segment to the map file
    
    ;; There are a few synthetic Q types (Qsep, Qconcat/src, and Qformal) that are
    ;; constructed    from the base Q types:
    ;;   Qsep:        takes a separator (e.g., "," or ";") and returns a procedure
    ;;                that constructs a qconcat with the separator placed between the
    ;;                elements.
    ;;   Qconcat/src: creates a Qconcat with a Qsegment on each end
    ;;   Qformal:     creates a Qconcat/src from an identifier, extracting the
    ;;                src and string to print from the identifier

    ;; A Qformal must be created before any references to the formal are created.

    (define-record-type Q
      (nongenerative)
      (fields (immutable size $Q-size)))
    (define (Q-size x)
      (cond
        [(string? x) (string-length x)]
        [(fixnum? x) 1]
        [(Q? x) ($Q-size x)]
        [else (internal-errorf 'Q-size "unrecognized Q ~s" x)]))
    (define-record-type Qconcat
      (nongenerative)
      (parent Q)
      (fields q*)
      (protocol
        (lambda (p->new)
          (lambda q*
            ((p->new (apply fx+ (map Q-size q*)))
             q*)))))
    (define-record-type Qsegment
      (nongenerative)
      (parent Q)
      (fields src start?)
      (protocol
        (lambda (p->new)
          (lambda (src start?)
            ((p->new 0)
             src start?)))))
    (define (make-Qsep sep)
      (case-lambda
        [() ""]
        [(q . q*)
         (apply make-Qconcat
           q
           (fold-right
             (lambda (q q*) (cons* sep 0 q q*))
             '()
             q*))]))
    (define (make-Qconcat/src src . q*)
      (apply make-Qconcat
        (cons
          (make-Qsegment src #t)
          (append q* (list (make-Qsegment src #f))))))
    (define (make-Qlocal! local)
      ; make-Qlocal! calls format-internal-binding, which must be called before
      ; format-id-reference is called on the same id
      (nanopass-case (Ltypescript Argument) local
        [(,var-name ,type)
         (make-Qconcat/src
           (id-src var-name)
           (format-internal-binding unique-local-name var-name))]))
    (define (make-Qlocals! . arg*)
      (apply (make-Qsep ",") (map make-Qlocal! arg*)))
    (define (make-Qformal! arg)
      ; make-Qformal! calls format-internal-binding, which must be called before
      ; format-id-reference is called on the same id
      (nanopass-case (Ltypescript Argument) arg
        [(,var-name ,type)
         (make-Qconcat/src
           (id-src var-name)
           (format-internal-binding unique-local-name var-name))]))
    (define (make-Qformals! . arg*)
      (apply (make-Qsep ",") (map make-Qformal! arg*)))
    (define (make-Qargs pure? q-formal*)
      (if pure?
          (apply (make-Qsep ",") q-formal*)
          (apply (make-Qsep ",") (cons* "context" "partialProofData" q-formal*))))

    (define (parenthesize required-level inner-level q)
      (if (>= inner-level required-level)
          q
          (make-Qconcat "(" q ")")))

    (define (print-indent indent) (printf "~v@t" indent))

    (define (for-each/sep use-sep p ls . ls*)
      (let loop ([ls ls] [ls* ls*] [sep ""])
        (unless (null? ls)
          (display-string sep)
          (apply p (car ls) (map car ls*))
          (loop (cdr ls) (map cdr ls*) use-sep))))

    (define (print-Q indent q)
      (define line-length 80)
      (print-indent indent)
      (let f ([q* (list q)] [col indent] [reset-col indent] [break? #f])
        (if (null? q*)
            col
            (let ([q (car q*)] [q* (cdr q*)])
              (cond
                [(fixnum? q)
                 (cond
                   [(null? q*) (f q* col reset-col break?)]
                   [(equal? (car q*) "") (f (cdr q*) col reset-col break?)]
                   [break?
                    (let ([col (fx+ reset-col q)])
                      (newline)
                      (print-indent col)
                      (f q* col reset-col break?))]
                   [else
                    (write-char #\space)
                    (f q* (fx+ col 1) reset-col break?)])]
                [(string? q)
                 (display-string q)
                 (f q* (fx+ col (Q-size q)) reset-col break?)]
                [(Qsegment? q)
                 (record-sourcemap-segment! sourcemap-tracker (Qsegment-src q) (Qsegment-start? q))
                 (f q* col reset-col break?)]
                [(Qconcat? q)
                 (let ([col (f (Qconcat-q* q) col col (fx> (fx+ col (Q-size q)) line-length))])
                   (f q* col reset-col break?))]
                [else (assert cannot-happen)])))))
    (define (de-alias type)
      (nanopass-case (Ltypescript Type) type
        [(talias ,src ,nominal? ,type-name ,type)
         (de-alias type)]
        [else type]))
    (define (public-adt? type)
      (nanopass-case (Ltypescript Type) (de-alias type)
        [(tadt ,src ,adt-name ([,adt-formal* ,adt-arg*] ...) ,vm-expr (,adt-op* ...) (,adt-rt-op* ...)) #t]
        [else #f]))
    )
  (Program : Program (ir) -> Program ()
    [(program ,src (,contract-type* ...) ((,export-name* ,name*) ...) (type-descriptors ,descriptor-table^ (,descriptor-id* ,type*) ...) ,pelt* ...)
     (let* ([xpelt* (maplr (lambda (x) (Program-Element x (map cons export-name* name*))) pelt*)]
            [uname* (maplr xpelt->uname xpelt*)])
       (print-contract.d.ts src xpelt* uname*)
       (fluid-let ([descriptor-table descriptor-table^])
         (print-contract.js src contract-type* descriptor-id* type* xpelt* uname*)))
     ir])
  (Program-Element : Program-Element (ir export-alist) -> * (xpelt)
    (definitions
      (define (external-names id)
        (fold-right
          (lambda (a external-name*)
            (if (eq? (cdr a) id)
                (cons (symbol->string (car a)) external-name*)
                external-name*))
          '()
          export-alist)))
    [(circuit ,src ,function-name (,arg* ...) ,type ,stmt)
     (unless (id-pure? function-name)
       (mark-function-async! function-name))
     (if (id-exported? function-name)
         (let ([external-name* (external-names function-name)])
           (XPelt-exported-circuit src function-name arg* type stmt external-name* (id-pure? function-name)))
         (XPelt-internal-circuit src function-name arg* type stmt (id-pure? function-name)))]
    [(witness ,src ,function-name (,arg* ...) ,type)
     (let ([external-name (symbol->string (id-sym function-name))])
       (XPelt-witness src function-name arg* type external-name))]
    [(native ,src ,function-name ,native-entry (,arg* ...) ,type)
     (let ([external-name (symbol->string (id-sym function-name))])
       (Xpelt-native-circuit src function-name native-entry arg* type external-name (id-pure? function-name)))]
    [(export-typedef ,src ,type-name (,tvar-name* ...) ,type)
     (let ([actual-type-name
            (nanopass-case (Ltypescript Type) type
              [(tstruct ,src ,struct-name (,elt-name* ,type*) ...) struct-name]
              [(tenum ,src ,enum-name ,elt-name ,elt-name* ...) enum-name]
              [(talias ,src ,nominal? ,type-name ,type) type-name]
              [else type-name])])
       (XPelt-type-definition src actual-type-name type-name tvar-name* type))]
    [(public-ledger-declaration ,pl-array ,lconstructor)
     (XPelt-public-ledger pl-array lconstructor external-names)]
    [,kdecl (XPelt-ledger-kernel)])
  (Untyped-Argument : Argument (ir) -> * (str)
    [(,var-name ,type)
     (format-internal-binding unique-local-name var-name)])
  (Typed-Argument : Argument (ir) -> * (str)
    [(,var-name ,[Type : type -> * type])
     (make-Qconcat (format-internal-binding unique-local-name var-name) ": " type)])
  (Stmt : Statement (ir return? outer-pure?) -> * (Q)
    [(if ,src ,[Expr : expr (precedence add1 none) outer-pure? -> * expr] ,[* stmt])
     (make-Qconcat
       (make-Qconcat
         "if ("
         expr
         ")"
         0 "{")
       2 stmt
       0 "}")]
    [(if ,src ,[Expr : expr (precedence add1 none) outer-pure? -> * expr] ,[* stmt1] ,[* stmt2])
     (make-Qconcat
       (make-Qconcat
         "if ("
         expr
         ")"
         0 "{")
       2 stmt1
       0 "} else {"
       2 stmt2
       0 "}")]
    [(seq ,src ,stmt* ... ,stmt)
     (let* ([stmt* (maplr (lambda (stmt) (Stmt stmt #f outer-pure?)) stmt*)]
            [stmt (Stmt stmt return? outer-pure?)]
            [stmt+ (if (and (> (length stmt*) 0) (equal? stmt "return;"))
                       stmt*
                       (append stmt* (list stmt)))])
       (apply make-Qconcat
              (car stmt+)
              (fold-right
                (lambda (stmt q*) (cons* 0 stmt q*))
                '()
                (cdr stmt+))))]
    [(const ,src ,local ,[Expr : expr (precedence add1 =) outer-pure? -> * expr])
     (make-Qconcat
       "const " 
       (make-Qlocal! local)
       " = "
       expr
       ";")]
    [(const ,src (,local* ...))
     (make-Qconcat
       "let " 
       (apply make-Qlocals! local*)
       ";")]
    [(statement-expression (tuple ,src))
     (guard (not return?))
     ""]
    [(statement-expression ,[Expr : expr (precedence add1 none) outer-pure? -> * expr])
     (if return?
         (make-Qconcat "return " expr ";")
         (make-Qconcat expr ";"))])
  (Expr : Expression (ir level outer-pure?) -> * (Q)
    (definitions
      (define (make-tuple tuple-arg*)
        (make-Qconcat
          "["
          (apply (make-Qsep ",")
            (map
              (lambda (tuple-arg)
                (nanopass-case (Ltypescript Tuple-Argument) tuple-arg
                  [(single ,src ,expr)
                   (Expr expr (precedence add1 comma) outer-pure?)]
                  [(spread ,src ,nat ,expr)
                   (make-Qconcat "..." (Expr expr (precedence add1 comma) outer-pure?))]))
              tuple-arg*))
          "]"))
      (define (downcast-unsigned src nat expr)
        (let ([expr (Expr expr (precedence add1 comma) outer-pure?)])
          (parenthesize level (precedence call)
            (make-Qconcat
              "((t1) => {"
              2 (format "if (t1 > ~an) {" nat)
              4 (format "throw new ~a('~a: cast from Field or Uint value to smaller Uint value failed: ' + t1 + ' is greater than ~a');"
                  (compact-stdlib "CompactError")
                  (format-source-object src)
                  nat)
              2 "}"
              2 "return t1;"
              0 "})("
              expr
              ")"))))
      (define (build-equal-helper! type equal-name)
        (define (build-equal-body type)
          (with-output-to-string
            (lambda ()
              (let f ([type type] [i 0] [indent 4])
                (nanopass-case (Ltypescript Type) (de-alias type)
                  [(topaque ,src ,opaque-type) (guard (string=? opaque-type "Uint8Array"))
                   (print-indent indent)
                   (printf "if (x~s.length != y~:*~s.length) { return false; }\n" i)
                   (print-indent indent)
                   (printf "if (!x~s.every((x, i) => y~:*~s[i] === x)) { return false; }\n" i)]
                  [(tbytes ,src ,len)
                   (print-indent indent)
                   (printf "if (!x~s.every((x, i) => y~:*~s[i] === x)) { return false; }\n" i)]
                  [(tvector ,src ,len ,type)
                   (print-indent indent)
                   (printf "for (let i~s = 0; i~:*~s < ~s; i~2:*~s++) {\n" i len)
                   (let ([next-i (fx+ i 1)] [next-indent (fx+ indent 2)])
                     (print-indent next-indent)
                     (printf "let x~s = x~s[i~:*~s];\n" next-i i)
                     (print-indent next-indent)
                     (printf "let y~s = y~s[i~:*~s];\n" next-i i)
                     (f type next-i next-indent))
                   (print-indent indent)
                   (printf "}\n")]
                  [(ttuple ,src ,type* ...)
                   (for-each
                     (lambda (eltno type)
                       (print-indent indent)
                       (printf "{\n")
                       (let ([next-i (fx+ i 1)] [next-indent (fx+ indent 2)])
                         (print-indent next-indent)
                         (printf "let x~s = x~s[~d];\n" next-i i eltno)
                         (print-indent next-indent)
                         (printf "let y~s = y~s[~d];\n" next-i i eltno)
                         (f type next-i next-indent))
                       (print-indent indent)
                       (printf "}\n"))
                     (enumerate type*)
                     type*)]
                  [(tstruct ,src ,struct-name (,elt-name* ,type*) ...)
                   (for-each
                     (lambda (elt-name type)
                       (print-indent indent)
                       (printf "{\n")
                       (let ([next-i (fx+ i 1)] [next-indent (fx+ indent 2)])
                         (print-indent next-indent)
                         (printf "let x~s = x~s.~s;\n" next-i i elt-name)
                         (print-indent next-indent)
                         (printf "let y~s = y~s.~s;\n" next-i i elt-name)
                         (f type next-i next-indent))
                       (print-indent indent)
                       (printf "}\n"))
                     elt-name*
                     type*)]
                  [(tpoint ,src (curve-jubjub))
                   (print-indent indent)
                   (printf "if (x~s.x != y~:*~s.x || x~:*~s.y != y~:*~s.y) {\n" i)
                   (print-indent (fx+ indent 2))
                   (printf "return false;\n")
                   (print-indent indent)
                   (printf "}\n")]
                  [(tpoint ,src (curve-secp256k1))
                   (print-indent indent)
                   (printf "if (x~s.identity) { return y~:*~s.identity; }\n" i)
                   (print-indent indent)
                   (printf "if (y~s.identity || x~:*~s.x != y~:*~s.x || x~:*~s.y != y~:*~s.y) {\n" i)
                   (print-indent (fx+ indent 2))
                   (printf "return false;\n")
                   (print-indent indent)
                   (printf "}\n")]
                  [else
                   (print-indent indent)
                   (printf "if (x~s !== y~:*~s) { return false; }\n" i)])))))
        (parenthesize level (precedence call)
          (set! helper*
            (cons
              (format "  ~a(x0, y0) {\n~a    return true;\n  }\n"
                equal-name
                (build-equal-body type))
              helper*))))
      )
    [(quote ,src ,datum)
     (cond
       [(field? datum) (format "~dn" datum)]
       [(boolean? datum) (if datum "true" "false")]
       [(bytevector? datum)
        (parenthesize level (precedence new)
          (format "new Uint8Array([~{~a~^, ~}])"
            (bytevector->u8-list datum)))]
       [else (assert cannot-happen)])]
    [(var-ref ,src ,var-name)
     (make-Qconcat/src src (format-id-reference var-name))]
    [(default ,src ,type)
     (let default-value ([type type])
       (let ([type (de-alias type)])
         (let ([type (subst-tcontract type)])
           (nanopass-case (Ltypescript Type) type
             [(tboolean ,src) "false"]
             [(tfield ,src ,ftype) "0n"]
             [(tunsigned ,src ,nat) "0n"]
             [(tpoint ,src ,ctype)
              (nanopass-case (Ltypescript Curve-Type) ctype
                [(curve-jubjub) "({x: 0n, y: 1n})"]
                [(curve-secp256k1) "({x: 0n, y: 0n, identity: true})"])]
             [(tbytes ,src ,len)
              (parenthesize level (precedence new)
                (format "new Uint8Array(~d)" len))]
             [(topaque ,src ,opaque-type)
              (case opaque-type
                [("string") "''"]
                [("Uint8Array") "new Uint8Array(0)"]
                ; FIXME: what should happen with other opaque types?
                [else (source-errorf src "opaque type ~a is not supported" opaque-type)])]
             [(tvector ,src ,len ,type)
              (parenthesize level (precedence new)
                (format "new Array(~a).fill(~a)"
                  len
                  (default-value type)))]
             [(ttuple ,src ,type* ...)
              (format "[~{~a~^, ~}]" (map default-value type*))]
             [(tstruct ,src ,struct-name (,elt-name* ,type*) ...)
              (format "{ ~{~a~^, ~} }"
                (map (lambda (elt-name type)
                       (format "~a: ~a" elt-name (default-value type)))
                     elt-name*
                     type*))]
             [(tenum ,src ,enum-name ,elt-name ,elt-name* ...) "0"]
             ; FIXME: this should not appear in the output at present, but might if we implement
             ; first-class ADT values
             [(tadt ,src ,adt-name ([,adt-formal* ,adt-arg*] ...) ,vm-expr (,adt-op* ...) (,adt-rt-op* ...))
              "undefined"]
             [else (assert cannot-happen)]))))]
    [(not ,src ,[Expr : expr (precedence not) outer-pure? -> * expr])
     (parenthesize level (precedence not)
       (make-Qconcat "!" expr))]
    [(and ,src ,[Expr : expr1 (precedence and) outer-pure? -> * expr1] ,[Expr : expr2 (precedence add1 and) outer-pure? -> * expr2])
     (parenthesize level (precedence and)
       (make-Qconcat expr1 0 "&&" 0 expr2))]
    [(or ,src ,[Expr : expr1 (precedence or) outer-pure? -> * expr1] ,[Expr : expr2 (precedence add1 or) outer-pure? -> * expr2])
     (parenthesize level (precedence or)
       (make-Qconcat expr1 0 "||" 0 expr2))]
    [(if ,src ,[Expr : expr0 (precedence add1 if) outer-pure? -> * expr0] ,[Expr : expr1 (precedence if) outer-pure? -> * expr1] ,[Expr : expr2 (precedence if) outer-pure? -> * expr2])
     (parenthesize level (precedence if)
       (make-Qconcat
         (make-Qconcat expr0 0 "?")
         0 (make-Qconcat expr1 0 ":")
         0 expr2))]
    [(elt-ref ,src ,[Expr : expr (precedence struct-ref) outer-pure? -> * expr] ,elt-name ,nat)
     (parenthesize level (precedence struct-ref)
       (make-Qconcat
         expr
         "."
         (format "~s" elt-name)))]
    [(emit ,src ,event-version ,event-tag ,len ,[Expr : expr (precedence add1 comma) outer-pure? -> * expr] ,vm-code)
     (let* ([bytes-type (with-output-language (Ltypescript Type) `(tbytes ,src ,len))]
            [vminstr*   (expand-vm-code src #f #f
                          `((emit-version . ,event-version)
                            (emit-tag     . ,event-tag)
                            (emit-payload . ,(make-vmref bytes-type expr)))
                          (vm-code-code vm-code))])
       (make-Qconcat
         "__compactRuntime.queryLedgerState("
         ((make-Qsep ",")
          "context"
          "partialProofData"
          (vminstr->q-array src vminstr*))
         ")"))]
    [(enum-ref ,src ,type ,elt-name^)
     (parenthesize level (precedence call)
       (nanopass-case (Ltypescript Type) (de-alias type)
         [(tenum ,src^ ,enum-name ,elt-name ,elt-name* ...)
          (let loop ([elt-name elt-name] [elt-name* elt-name*] [i 0])
            (if (eq? elt-name elt-name^)
                (format "~d" i)
                (begin
                  (assert (not (null? elt-name*)))
                  (loop (car elt-name*) (cdr elt-name*) (fx+ i 1)))))]
         [else (assert cannot-happen)]))]
    [(tuple ,src ,tuple-arg* ...)
     (make-tuple tuple-arg*)]
    [(vector ,src ,tuple-arg* ...)
     (make-tuple tuple-arg*)]
    [(tuple-ref ,src ,[Expr : expr (precedence vector-ref) outer-pure? -> * expr] ,kindex)
     (parenthesize level (precedence vector-ref)
       (make-Qconcat expr (format "[~d]" kindex)))]
    [(bytes-ref ,src ,type ,[Expr : expr (precedence vector-ref) outer-pure? -> * expr] ,[Expr : index (precedence add1 comma) outer-pure? -> * index])
     ; NB: counting on check in optimize/resolve-indices to guarantee that the computed
     ; index cannot be out-of-range
     (parenthesize level (precedence vector-ref)
       (make-Qconcat "BigInt(" expr "[" index "])"))]
    [(vector-ref ,src ,type ,[Expr : expr (precedence vector-ref) outer-pure? -> * expr] ,[Expr : index (precedence add1 comma) outer-pure? -> * index])
     ; NB: counting on check in optimize/resolve-indices to guarantee that the computed
     ; index cannot be out-of-range
     (parenthesize level (precedence vector-ref)
       (make-Qconcat expr "[" index "]"))]
    [(tuple-slice ,src ,type ,[Expr : expr (precedence add1 comma) outer-pure? -> * expr] ,kindex ,len)
     (parenthesize level (precedence call)
       (make-Qconcat
         (format "((e) => e.slice(~d, ~d))(" kindex (+ kindex len))
         expr
         ")"))]
    [(bytes-slice ,src ,type ,[Expr : expr (precedence add1 comma) outer-pure? -> * expr] ,[Expr : index (precedence add1 comma) outer-pure? -> * index] ,len)
     ; NB: counting on check in optimize/resolve-indices to guarantee that the computed
     ; index + len cannot be out-of-range
     (parenthesize level (precedence call)
       (make-Qconcat
         (format "((e, i) => e.slice(i, i+~d))(" len)
         ((make-Qsep ",") expr (make-Qconcat "Number(" index ")"))
         ")"))]
    [(vector-slice ,src ,type ,[Expr : expr (precedence add1 comma) outer-pure? -> * expr] ,[Expr : index (precedence add1 comma) outer-pure? -> * index] ,len)
     ; NB: counting on check in optimize/resolve-indices to guarantee that the computed
     ; index + len cannot be out-of-range
     (parenthesize level (precedence call)
       (make-Qconcat
         (format "((e, i) => e.slice(i, i+~d))(" len)
         ((make-Qsep ",") expr (make-Qconcat "Number(" index ")"))
         ")"))]
    [(+ ,src (tunsigned ,src^ ,nat) ,[Expr : expr1 (precedence +) outer-pure? -> * expr1]
                                    ,[Expr : expr2 (precedence add1 +) outer-pure? -> * expr2])
     (parenthesize level (precedence +)
       ;; infer-types guarantees that the result is in range via range analysis.
       (make-Qconcat expr1 0 "+" 0 expr2))]
    [(+ ,src ,type ,[Expr : expr1 (precedence add1 comma) outer-pure? -> * expr1]
                   ,[Expr : expr2 (precedence add1 comma) outer-pure? -> * expr2])
     (let ([fun (nanopass-case (Ltypescript Type) type
                  ;; infer-types guarantees that these are the only possibilities.
                  [(tfield ,src^ (field-native)) "addField"]
                  [(tfield ,src^ (field-base (curve-secp256k1))) "secp256k1BaseAdd"]
                  [(tfield ,src^ (field-scalar (curve-secp256k1))) "secp256k1ScalarAdd"]
                  [else (assert cannot-happen)])])
       (parenthesize level (precedence call)
         (make-Qconcat
           (compact-stdlib fun)
           "("
           ((make-Qsep ",") expr1 expr2)
           ")")))]
    [(- ,src (tunsigned ,src^ ,nat) ,[Expr : expr1 (precedence -) outer-pure? -> * expr1]
                                    ,[Expr : expr2 (precedence add1 -) outer-pure? -> * expr2])
     (parenthesize level (precedence -)
       ;; infer-type guarantees that the result isn't negative by inserting a run-time check.
       (make-Qconcat expr1 0 "-" 0 expr2))]
    [(- ,src ,type ,[Expr : expr1 (precedence add1 comma) outer-pure? -> * expr1]
                   ,[Expr : expr2 (precedence add1 comma) outer-pure? -> * expr2])
     (let ([fun (nanopass-case (Ltypescript Type) type
                  ;; infer-types guarantees that these are the only possibilities.
                  [(tfield ,src^ (field-native)) "subField"]
                  [(tfield ,src^ (field-base (curve-secp256k1))) "secp256k1BaseSub"]
                  [(tfield ,src^ (field-scalar (curve-secp256k1))) "secp256k1ScalarSub"]
                  [else (assert cannot-happen)])])
       (parenthesize level (precedence call)
         (make-Qconcat
           (compact-stdlib fun)
           "("
           ((make-Qsep ",") expr1 expr2)
           ")")))]
    [(* ,src (tunsigned ,src^ ,nat) ,[Expr : expr1 (precedence *) outer-pure? -> * expr1]
                                    ,[Expr : expr2 (precedence add1 *) outer-pure? -> * expr2])
     (parenthesize level (precedence *)
       ;; infer-type guarantees that the result is in range via range analysis.
       (make-Qconcat expr1 0 "*" 0 expr2))]
    [(* ,src ,type ,[Expr : expr1 (precedence add1 comma) outer-pure? -> * expr1]
                   ,[Expr : expr2 (precedence add1 comma) outer-pure? -> * expr2])
     (let ([fun (nanopass-case (Ltypescript Type) type
                  ;; infer-types guarantees that these are the only possibilities.
                  [(tfield ,src^ (field-native)) "mulField"]
                  [(tfield ,src^ (field-base (curve-secp256k1))) "secp256k1BaseMul"]
                  [(tfield ,src^ (field-scalar (curve-secp256k1))) "secp256k1ScalarMul"]
                  [else (assert cannot-happen)])])
       (parenthesize level (precedence call)
         (make-Qconcat
           (compact-stdlib fun)
           "("
           ((make-Qsep ",") expr1 expr2)
           ")")))]
    [(< ,src ,bits ,[Expr : expr1 (precedence <) outer-pure? -> * expr1] ,[Expr : expr2 (precedence add1 <) outer-pure? -> * expr2])
     (parenthesize level (precedence <)
       (make-Qconcat expr1 0 "<" 0 expr2))]
    [(<= ,src ,bits ,[Expr : expr1 (precedence <=) outer-pure? -> * expr1] ,[Expr : expr2 (precedence add1 <=) outer-pure? -> * expr2])
     (parenthesize level (precedence <=)
       (make-Qconcat expr1 0 "<=" 0 expr2))]
    [(> ,src ,bits ,[Expr : expr1 (precedence >) outer-pure? -> * expr1] ,[Expr : expr2 (precedence add1 >) outer-pure? -> * expr2])
     (parenthesize level (precedence >)
       (make-Qconcat expr1 0 ">" 0 expr2))]
    [(>= ,src ,bits ,[Expr : expr1 (precedence >=) outer-pure? -> * expr1] ,[Expr : expr2 (precedence add1 >=) outer-pure? -> * expr2])
     (parenthesize level (precedence >=)
       (make-Qconcat expr1 0 ">=" 0 expr2))]
    [(== ,src ,type ,expr1 ,expr2)
     (if (nanopass-case (Ltypescript Type) (de-alias type)
           [(tboolean ,src) #t]
           [(tfield ,src ,ftype) #t]
           [(tunsigned ,src ,nat) #t]
           [(topaque ,src ,opaque-type) (string=? opaque-type "string")]
           [(tenum ,src ,enum-name ,elt-name ,elt-name* ...) #t]
           [else #f])
         (parenthesize level (precedence ==)
           (make-Qconcat
             (Expr expr1 (precedence ==) outer-pure?)
             0 "==="
             0 (Expr expr2 (precedence add1 ==) outer-pure?)))
         (parenthesize level (precedence call)
           (let ([equal-name (unique-global-name "equal")])
             (build-equal-helper! type equal-name)
             (make-Qconcat
               "this."
               equal-name
               "("
               ((make-Qsep ",")
                (Expr expr1 (precedence add1 comma) outer-pure?)
                (Expr expr2 (precedence add1 comma) outer-pure?))
               ")"))))]
    [(!= ,src ,type ,expr1 ,expr2)
     (if (nanopass-case (Ltypescript Type) (de-alias type)
           [(tboolean ,src) #t]
           [(tfield ,src ,ftype) #t]
           [(tunsigned ,src ,nat) #t]
           [(topaque ,src ,opaque-type) (string=? opaque-type "string")]
           [(tenum ,src ,enum-name ,elt-name ,elt-name* ...) #t]
           [else #f])
         (parenthesize level (precedence ==)
           (make-Qconcat
             (Expr expr1 (precedence ==) outer-pure?)
             0 "!=="
             0 (Expr expr2 (precedence add1 ==) outer-pure?)))
         (parenthesize level (precedence not)
           (let ([equal-name (unique-global-name "equal")])
             (build-equal-helper! type equal-name)
             (make-Qconcat
               "!this."
               equal-name
               "("
               ((make-Qsep ",")
                (Expr expr1 (precedence add1 comma) outer-pure?)
                (Expr expr2 (precedence add1 comma) outer-pure?))
               ")"))))]
    [(map ,src ,len ,[Function : fun0 outer-pure? -> * fun]
          ,[Map-Argument : map-arg (precedence add1 comma) outer-pure? -> * expr byte-ref?]
          ,[Map-Argument : map-arg* (precedence add1 comma) outer-pure? -> * expr* byte-ref?*]
          ...)
     (let ([mapper-name (unique-global-name "mapper")]
           [pure? (nanopass-case (Ltypescript Function) fun0
                    [(fref ,src ,function-name) (id-pure? function-name)]
                    [else outer-pure?])]
           [async? (nanopass-case (Ltypescript Function) fun0
                     [(fref ,src ,function-name) (function-async? function-name)]
                     [else (not outer-pure?)])])
       (set! helper*
         (cons
           (let ([i+ (enumerate (cons expr expr*))])
             (format "  ~a~a(~@[~*context, partialProofData, ~]f, ~{~a~^, ~}) {\n    let a = [];\n    for (let i = 0; i < ~a; i++) { a[i] = ~af(~@[~*context, partialProofData, ~]~{~a~^, ~}); }\n    return a;\n  }\n"
               (if async? "async " "")
               mapper-name
               (not pure?)
               (map (lambda (i) (format "a~s" i)) i+)
               len
               (if async? "await " "")
               (not pure?)
               (map (lambda (i byte-ref?)
                      (let ([ref (format "a~d[i]" i)])
                        (if byte-ref? (format "BigInt(~a)" ref) ref)))
                    i+
                    (cons byte-ref? byte-ref?*))))
           helper*))
       (let ([call-q
              (make-Qconcat
                "this."
                mapper-name
                "("
                (make-Qargs pure?
                  (cons*
                    (nanopass-case (Ltypescript Function) fun0
                      [(fref ,src ,function-name)
                       (let ([args (format-internal-binding unique-local-name (make-temp-id src 'args))])
                         (make-Qconcat "(..." args ") =>" 2 fun "(..." args ")"))]
                      [else fun])
                    expr
                    expr*))
                ")")])
         (if async?
             (parenthesize level (precedence not)
               (make-Qconcat "await " call-q))
             (parenthesize level (precedence call) call-q))))]
    [(fold ,src ,len ,[Function : fun0 outer-pure? -> * fun]
           (,[Expr : expr0 (precedence add1 comma) outer-pure? -> * expr0] ,type)
           ,[Map-Argument : map-arg (precedence add1 comma) outer-pure? -> * expr byte-ref?]
           ,[Map-Argument : map-arg* (precedence add1 comma) outer-pure? -> * expr* byte-ref?*]
           ...)
     (let ([folder-name (unique-global-name "folder")]
           [pure? (nanopass-case (Ltypescript Function) fun0
                    [(fref ,src ,function-name) (id-pure? function-name)]
                    [else outer-pure?])]
           [async? (nanopass-case (Ltypescript Function) fun0
                     [(fref ,src ,function-name) (function-async? function-name)]
                     [else (not outer-pure?)])])
       (set! helper*
         (cons
           (let ([i+ (enumerate (cons expr expr*))])
             (format "  ~a~a(~@[~*context, partialProofData, ~]f, x, ~{~a~^, ~}) {\n    for (let i = 0; i < ~a; i++) { x = ~af(~@[~*context, partialProofData, ~]x, ~{~a~^, ~}); }\n    return x;\n  }\n"
               (if async? "async " "")
               folder-name
               (not pure?)
               (map (lambda (i) (format "a~s" i)) i+)
               len
               (if async? "await " "")
               (not pure?)
               (map (lambda (i byte-ref?)
                      (let ([ref (format "a~d[i]" i)])
                        (if byte-ref? (format "BigInt(~a)" ref) ref)))
                    i+
                    (cons byte-ref? byte-ref?*))))
           helper*))
       (let ([call-q
              (make-Qconcat
                "this."
                folder-name
                "("
                (make-Qargs pure?
                  (cons*
                    (nanopass-case (Ltypescript Function) fun0
                      [(fref ,src ,function-name)
                       (let ([args (format-internal-binding unique-local-name (make-temp-id src 'args))])
                         (make-Qconcat "(..." args ") =>" 2 fun "(..." args ")"))]
                      [else fun])
                    expr0
                    expr
                    expr*))
                ")")])
         (if async?
             (parenthesize level (precedence not)
               (make-Qconcat "await " call-q))
             (parenthesize level (precedence call) call-q))))]
    [(call ,src ,function-name ,[Expr : expr* (precedence add1 comma) outer-pure? -> * expr*] ...)
     (if (function-async? function-name)
         (parenthesize level (precedence not)
           (make-Qconcat
             "await "
             (format-function-reference src function-name)
             "("
             (make-Qargs (id-pure? function-name) expr*)
             ")"))
         (parenthesize level (precedence call)
           (make-Qconcat
             (format-function-reference src function-name)
             "("
             (make-Qargs (id-pure? function-name) expr*)
             ")")))]
    [(new ,src ,type ,[Expr : expr* (precedence add1 comma) outer-pure? -> * expr*] ...)
     (nanopass-case (Ltypescript Type) (de-alias type)
       [(tstruct ,src ,struct-name (,elt-name* ,type*) ...)
        (make-Qconcat
          "{ "
          (apply (make-Qsep ",")
            (map (lambda (elt-name expr)
                   (make-Qconcat (format "~a" elt-name) ":" 2 expr))
                 elt-name*
                 expr*))
          " }")]
       [else (assert cannot-happen)])]
    ; can't happen because no one-element seqs are produced by upstream passes
    [(seq ,src ,[Expr : expr level outer-pure? -> * expr]) expr]
    [(seq ,src ,[Expr : expr* (precedence add1 comma) outer-pure? -> * expr*] ... ,[Expr : expr (precedence add1 comma) outer-pure? -> * expr])
     (parenthesize level (precedence comma)
       (apply (make-Qsep ",") (append expr* (list expr))))]
    [(= ,src ,var-name ,[Expr : expr (precedence =) outer-pure? -> * expr])
     (parenthesize level (precedence =)
       (make-Qconcat
         (format-id-reference var-name)
         " = "
         expr))]
    [(assert ,src ,[Expr : expr (precedence add1 comma) outer-pure? -> * expr] ,mesg)
     (parenthesize level (precedence call)
       (make-Qconcat
         (compact-stdlib "assert")
         "("
         ((make-Qsep ",")
           expr
           (format-javascript-string mesg))
         ")"))]
    [(field->bytes ,src ,len ,ftype ,[Expr : expr (precedence add1 comma) outer-pure? -> * expr])
     (parenthesize level (precedence call)
       (make-Qconcat
         (compact-stdlib "convertBigintToBytes")
         "("
         ((make-Qsep ",") (format "~d" len) expr (format "'~a'" (format-source-object src)))
         ")"))]
    [(cast-from-bytes ,src ,type ,len ,[Expr : expr (precedence add1 comma) outer-pure? -> * expr])
     (parenthesize level (precedence call)
       (let-values ([(convert max)
                     (nanopass-case (Ltypescript Type) (de-alias type)
                       [(tfield ,src^ (field-native))
                        ;; convertBytesToUint is used intentionally in order to fail on values
                        ;; that are larger than `Field`'s maximum value.
                        (values "convertBytesToUint" (max-field))]
                       [(tfield ,src^ (field-scalar (curve-jubjub)))
                        (values "convertBytesToField" (max-jubjub-scalar))]
                       [(tfield ,src^ (field-base (curve-secp256k1)))
                        (values "convertBytesToField" (max-secp256k1-base))]
                       [(tfield ,src^ (field-scalar (curve-secp256k1)))
                        (values "convertBytesToField" (max-secp256k1-scalar))]
                       [(tunsigned ,src^ ,nat)
                        (values "convertBytesToUint" nat)])])
         (make-Qconcat (compact-stdlib convert) "("
           ((make-Qsep ",")
            (format "~dn" max)
            (format "~d" len)
            expr
            (format "'~a'" (format-type type))
            (format "'~a'" (format-source-object src)))
           ")")))]
    [(vector->bytes ,src ,len ,[Expr : expr (precedence add1 comma) outer-pure? -> * expr])
     (parenthesize level (precedence call)
       (make-Qconcat
         "Uint8Array.from("
         ((make-Qsep ",") expr "Number")
         ")"))]
    [(bytes->vector ,src ,len ,[Expr : expr (precedence add1 comma) outer-pure? -> * expr])
     (parenthesize level (precedence call)
       (make-Qconcat
         "Array.from("
         ((make-Qsep ",") expr "BigInt")
         ")"))]
    [(cast-from-enum ,src ,type ,type^ ,[Expr : expr (precedence add1 comma) outer-pure? -> * expr])
     (parenthesize level (precedence call)
       (let-values ([(enum-name maxval)
                     (nanopass-case (Ltypescript Type) (de-alias type^)
                       [(tenum ,src ,enum-name ,elt-name ,elt-name* ...)
                        (values enum-name (length elt-name*))]
                       [else (assert cannot-happen)])])
         (cond
           [(nanopass-case (Ltypescript Type) (de-alias type)
              [(tfield ,src ,ftype) #f]
              [(tunsigned ,src ,nat) (guard (< nat maxval)) nat]
              [else #f]) =>
            (lambda (nat)
              (make-Qconcat
                "((t1) => {"
                2 (format "if (t1 > ~d) {" nat)
                4 (format "throw new ~a('~a: cast from enum ~a to Uint<0..~d> failed: enum value ' + t1 + ' is greater than ~d');"
                    (compact-stdlib "CompactError")
                    (format-source-object src)
                    enum-name
                    (+ nat 1)
                    nat)
                2 "}"
                2 "return BigInt(t1);"
                0 "})("
                expr
                ")"))]
           [else (make-Qconcat "BigInt(" expr ")")])))]
    [(cast-to-enum ,src ,type ,type^ ,[Expr : expr (precedence add1 comma) outer-pure? -> * expr])
     (parenthesize level (precedence call)
       (let-values ([(enum-name maxval)
                     (nanopass-case (Ltypescript Type) (de-alias type)
                       [(tenum ,src ,enum-name ,elt-name ,elt-name* ...)
                        (values enum-name (length elt-name*))]
                       [else (assert cannot-happen)])])
         (if (nanopass-case (Ltypescript Type) (de-alias type)
               [(tunsigned ,src ,nat) (<= nat maxval)]
               [else #f])
             (make-Qconcat "Number(" expr ")")
             (make-Qconcat
               "((t1) => {"
               2 (format "if (t1 > ~dn) {" maxval)
               4 (format "throw new ~a('~a: cast from Field or Uint value to enum ~a failed: ' + t1 + ' is greater than maximum enum value ~dn');"
                   (compact-stdlib "CompactError")
                   (format-source-object src)
                   enum-name
                   maxval)
               2 "}"
               2 "return Number(t1);"
               0 "})("
               expr
               ")"))))]
    [(cast-to-field ,src (field-native) ,type ,expr)
     ;; Foreign fields and unsigned integers are the same type as the native field (bigint), and
     ;; all possible values are smaller than the native field modulus.
     (Expr expr level outer-pure?)]
    [(cast-to-field ,src (field-scalar (curve-jubjub)) ,type ,expr)
     (if (feature-zkir-v3)
         (parenthesize level (precedence call)
           (make-Qconcat
             (compact-stdlib "convertNumericToJubjubScalar")
             "("
             (Expr expr (precedence add1 comma) outer-pure?)
             ")"))
         (Expr expr level outer-pure?))]
    [(cast-to-field ,src ,ftype ,type ,expr)
     (assert cannot-happen)]
    [(cast-from-field ,src ,nat ,ftype ,expr)
     (downcast-unsigned src nat expr)]
    [(downcast-unsigned ,src ,nat? ,nat ,expr)
     (downcast-unsigned src nat expr)]
    [(safe-cast ,src ,type ,type^ ,expr)
     ; no checks needed for safe casts
     (Expr expr level outer-pure?)]
    [(public-ledger ,src ,ledger-field-name ,sugar? (,path-elt* ...) ,src^ ,adt-op ,[Expr : expr* (precedence add1 comma) outer-pure? -> * expr*] ...)
     (nanopass-case (Ltypescript ADT-Op) adt-op
       [(,ledger-op ,op-class (,adt-name (,adt-formal* ,adt-arg*) ...) ((,var-name* ,type*) ...) ,type ,vm-code)
        ; this should be caught by ledger meta-type checks or by propagate-ledger-paths
        (when (public-adt? type) (source-errorf src "incomplete reference to nested ADT"))
        ;; For a tcontract result look up the ContractAddress descriptor that
        ;; subst-tcontract substituted for during register-descriptor!.  For other
        ;; types keep the existing descriptor lookup.
        (let ([descriptor-name?
                (and (eq? op-class 'read)
                     (type->maybe-descriptor-name (subst-tcontract type)))])
          (let ([q (construct-query src path-elt* adt-formal* adt-arg* adt-op expr*)])
            (if descriptor-name?
                (make-Qconcat
                  descriptor-name?
                  ".fromValue("
                  q
                  ".value)")
                q)))])]
    [(contract-call ,src ,elt-name (,[Expr : expr (precedence add1 comma) outer-pure? -> * expr] ,type) ,[Expr : expr* (precedence add1 comma) outer-pure? -> * expr*] ...)
     ;; Lower a cross-contract call to:
     ;;   await __compactRuntime.crossContractCall({
     ;;     context,
     ;;     interfaceName: '<contract-name>',
     ;;     declaration: declaredInterfaces['<contract-name>'],
     ;;     calleeCircuitId: '<elt-name>',
     ;;     calleeAddress: <receiver-expr>,
     ;;     partialProofData,
     ;;     args: [<args>...]})
     (when outer-pure?
       (source-errorf src "cross-contract call from a pure circuit is not yet supported"))
     (nanopass-case (Ltypescript Type) (de-alias type)
       [(tcontract ,src^ ,contract-name (,elt-name* ,pure-dcl* (,type** ...) ,type*) ...)
        ;; Type checking already established this; re-checked so a declaration missing the circuit
        ;; fails here rather than as a run-time conformance failure.
        (unless (memq elt-name elt-name*)
          (internal-errorf 'print-typescript
            "contract-call references unknown circuit ~s on contract ~s"
            elt-name contract-name))
        (let ([interface-name (format-javascript-string (symbol->string contract-name))]
              [callee-address
               (make-Qconcat
                 (format "~a((" (compact-stdlib "decodeContractAddress"))
                 expr
                 ").bytes)")])
          (parenthesize level (precedence not)
            (make-Qconcat
              (format "await ~a({" (compact-stdlib "crossContractCall"))
              1 ((make-Qsep ",")
                 "context"
                 (format "interfaceName: ~a" interface-name)
                 (format "declaration: declaredInterfaces[~a]" interface-name)
                 (format "calleeCircuitId: ~a" (format-javascript-string (symbol->string elt-name)))
                 (make-Qconcat "calleeAddress: " callee-address)
                 "partialProofData"
                 (make-Qconcat "args: [" (apply (make-Qsep ",") expr*) "]"))
              0 "})")))]
       [else
        (source-errorf src "internal: contract-call type is not a tcontract")])]
    [(verify-proof ,src
       ,vk
       ,[Expr : expr1 (precedence add1 comma) outer-pure? -> * expr1]
       ,[Expr : expr2 (precedence add1 comma) outer-pure? -> * expr2])
     ;; The verifying key goes out as hex rather than as the record itself: the
     ;; Q printer takes only strings, fixnums and Qs.
     (parenthesize level (precedence call)
       (make-Qconcat
         (compact-stdlib "verifyProof")
           "("
           ((make-Qsep ",")
              "partialProofData"
              (inner-vk-hex vk)
              expr1
              expr2)
           ")"))])
  (Map-Argument : Map-Argument (ir level outer-pure?) -> * (Q byte-ref?)
    [(,[Expr : expr (precedence add1 comma) outer-pure? -> * expr] ,type ,type^)
     (values
       expr
       (nanopass-case (Ltypescript Type) (de-alias type)
         [(tbytes ,src ,len) #t]
         [else #f]))])
  (Function : Function (ir outer-pure?) -> * (str)
    [(fref ,src ,function-name) (format-function-reference src function-name)]
    [(circuit ,src (,arg* ...) ,type ,stmt)
     (let ([q-formal* (map make-Qformal! arg*)])
       (make-Qconcat
         "("
         (make-Qconcat
           (if outer-pure? "(" "async (")
           (make-Qargs outer-pure? q-formal*)
           ") =>"
           0 "{"
           2 (Stmt stmt #t outer-pure?)
           0 "}")
         ")"))])
  (Type : Type (ir) -> * (str)
    (definitions
      (define (same-type? type1 type2)
        (and (unify-type '() type1 type2) #t))
      (define (curve-type=? ctype1 ctype2)
        (nanopass-case (Ltypescript Curve-Type) ctype1
          [(curve-jubjub)
           (nanopass-case (Ltypescript Curve-Type) ctype2
             [(curve-jubjub) #t]
             [else #f])]
          [(curve-secp256k1)
           (nanopass-case (Ltypescript Curve-Type) ctype2
             [(curve-secp256k1) #t]
             [else #f])]))
      (define (unify-type tvar-name* type1 type2)
        (define-syntax T
          (syntax-rules ()
            [(T ty clause ...)
             (nanopass-case (Ltypescript Type) ty clause ... [else #f])]))
        (let ([subst* (map (lambda (x) (cons x #f)) tvar-name*)])
          (and (let unify? ([type1 type1] [type2 type2])
                 (nanopass-case (Ltypescript Type) type1
                   [,tvar-name1
                    (cond
                      [(assq tvar-name1 subst*) =>
                       (lambda (a)
                         (if (eq? (cdr a) #f)
                             (begin (set-cdr! a type2) #t)
                             (same-type? (cdr a) type2)))]
                      [else
                       ; can't happen at present, since type2 should not contain tvar refs
                       (T type2 [,tvar-name2 (eq? tvar-name1 tvar-name2)])])]
                   [(tboolean ,src) (T type2 [(tboolean ,src) #t])]
                   [(tfield ,src ,ftype1)
                    (T type2
                       [(tfield ,src ,ftype2) #t]
                       [(tunsigned ,src ,nat) #t])]
                   [(tunsigned ,src ,nat1)
                    (T type2
                       [(tunsigned ,src ,nat2) #t]
                       [(tfield ,src ,ftype) #t])]
                   [(tpoint ,src1 ,ctype1)
                    (T type2 [(tpoint ,src2 ,ctype2) (curve-type=? ctype1 ctype2)])]
                   [(tbytes ,src ,len1) (T type2 [(tbytes ,src ,len2) #t])]
                   [(topaque ,src ,opaque-type1) (T type2 [(topaque ,src ,opaque-type2) (string=? opaque-type1 opaque-type2)])]
                   [(tvector ,src ,len1 ,type1) (T type2 [(tvector ,src ,len2 ,type2) (unify? type1 type2)])]
                   [(tcontract ,src1 ,contract-name1 (,elt-name1* ,pure-dcl1* (,type1** ...) ,type1*) ...)
                    (T type2
                       [(tcontract ,src2 ,contract-name2 (,elt-name2* ,pure-dcl2* (,type2** ...) ,type2*) ...)
                        (define (circuit-superset? elt-name1* pure-dcl1* type1** type1* elt-name2* pure-dcl2* type2** type2*)
                          (andmap (lambda (elt-name2 pure-dcl2 type2* type2)
                                    (ormap (lambda (elt-name1 pure-dcl1 type1* type1)
                                             (and (eq? elt-name1 elt-name2)
                                                  (eq? pure-dcl1 pure-dcl2)
                                                  (fx= (length type1*) (length type2*))
                                                  (andmap unify? type1* type2*)
                                                  (unify? type1 type2)))
                                      elt-name1* pure-dcl1* type1** type1*))
                            elt-name2* pure-dcl2* type2** type2*))
                         (and (eq? contract-name1 contract-name2)
                              (fx= (length elt-name1*) (length elt-name2*))
                              (circuit-superset? elt-name1* pure-dcl1* type1** type1* elt-name2* pure-dcl2* type2** type2*))])]
                   [(ttuple ,src ,type1* ...)
                    (T type2
                       [(ttuple ,src ,type2* ...)
                        (and (fx= (length type1*) (length type2*))
                             (andmap unify? type1* type2*))])]
                   [(tstruct ,src ,struct-name1 (,elt-name1* ,type1*) ...)
                    (T type2
                       [(tstruct ,src ,struct-name2 (,elt-name2* ,type2*) ...)
                        (and (eq? struct-name1 struct-name2)
                             (fx= (length elt-name1*) (length elt-name2*))
                             (andmap eq? elt-name1* elt-name2*)
                             (andmap unify? type1* type2*))])]
                   [(tenum ,src ,enum-name1 ,elt-name1 ,elt-name1* ...)
                    (T type2
                       [(tenum ,src ,enum-name2 ,elt-name2 ,elt-name2* ...)
                        (and (eq? enum-name1 enum-name2)
                             (eq? elt-name1 elt-name2)
                             (fx= (length elt-name1*) (length elt-name2*))
                             (andmap eq? elt-name1* elt-name2*))])]
                   [(talias ,src1 ,nominal1? ,type-name1 ,type1)
                    (T type2
                       [(talias ,src2 ,nominal2? ,type-name2 ,type2)
                        (and (eq? type-name1 type-name2)
                             (unify? type1 type2))])]
                   [else (internal-errorf 'print-typescript
                                          "unhandled type ~a in same-type?"
                                          type1)]))
               (map cdr subst*)))))
    [,tvar-name (symbol->string tvar-name)]
    [(tboolean ,src) "boolean"]
    [(tfield ,src ,ftype) "bigint"]
    [(tunsigned ,src ,nat) "bigint"]
    [(tpoint ,src ,ctype)
     (nanopass-case (Ltypescript Curve-Type) ctype
       [(curve-jubjub) "__compactRuntime.JubjubPoint"]
       [(curve-secp256k1) "__compactRuntime.Secp256k1Point"])]
    [(tbytes ,src ,len) "Uint8Array"]
    [(topaque ,src ,opaque-type)
     (case opaque-type
       [("string" "Uint8Array") opaque-type]
       ;; FIXME: what should happen with other opaque types?
       [else (source-errorf src "opaque type ~a is not supported" opaque-type)])]
    [(tvector ,src ,len ,[Type : type -> * type])
     (make-Qconcat type "[]")]
    [(tcontract ,src ,contract-name (,elt-name* ,pure-dcl* (,type** ...) ,type*) ...)
     "{ bytes: Uint8Array }"]
    [(ttuple ,src ,type* ...)
     (make-Qconcat
       "["
       (apply (make-Qsep ",")
         (map Type type*))
       "]")]
    [(tstruct ,src ,struct-name (,elt-name* ,type*) ...)
     (or (ormap (lambda (tinfo)
                  (cond
                    [(unify-type (tinfo-tvar* tinfo) (tinfo-type tinfo) ir) =>
                     (lambda (maybe-type*)
                       (let ([q (format "~a" (tinfo-export-name tinfo))])
                         (if (null? maybe-type*)
                             q
                             (make-Qconcat
                               q
                               "<"
                               (apply (make-Qsep ",")
                                      (map (lambda (maybe-type)
                                             (if maybe-type
                                                 (Type maybe-type)
                                                 "any"))
                                           maybe-type*))
                               ">"))))]
                    [else #f]))
                (hashtable-ref exported-type-ht struct-name '()))
         (make-Qconcat
           "{ "
           (apply (make-Qsep ",")
                  (map (lambda (elt-name type)
                         (make-Qconcat (format "~a" elt-name) ": " (Type type)))
                       elt-name*
                       type*))
           0 "}"))]
    [(tenum ,src ,enum-name ,elt-name ,elt-name* ...)
     (or (ormap (lambda (tinfo)
                  (and (null? (tinfo-tvar* tinfo))
                       (same-type? (tinfo-type tinfo) ir)
                       (symbol->string (tinfo-export-name tinfo))))
                (hashtable-ref exported-type-ht enum-name '()))
         ; FIXME: we could create a new global definition with a unique global name generated
         ;        from enum-name to avoid using just "number" as the type, preferably avoiding duplicates
         "number")]
    [(talias ,src ,nominal? ,type-name ,type)
     (or (ormap (lambda (tinfo)
                  (cond
                    [(unify-type (tinfo-tvar* tinfo) (tinfo-type tinfo) ir) =>
                     (lambda (maybe-type*)
                       (let ([q (format "~a" (tinfo-export-name tinfo))])
                         (if (null? maybe-type*)
                             q
                             (make-Qconcat
                               q
                               "<"
                               (apply (make-Qsep ",")
                                      (map (lambda (maybe-type)
                                             (if maybe-type
                                                 (Type maybe-type)
                                                 "any"))
                                           maybe-type*))
                               ">"))))]
                    [else #f]))
                (hashtable-ref exported-type-ht type-name '()))
         (Type type))]
    [else (assert cannot-happen)]))
